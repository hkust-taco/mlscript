:global
:bbml


123
//│ Type: Int

3.14
//│ Type: Num

false
//│ Type: Bool

"bbml"
//│ Type: Str

()
//│ Type: ⊤

fun id(x) = x
id
//│ Type: α2_1 ->{⊥} α2_1

fun inc(x) = x + 1
//│ Type: ⊤

fun mul(x, y) = x * y
//│ Type: ⊤

x => x
//│ Type: (α8_1) ->{⊥} α8_1

+
//│ Type: (Int, Int) ->{⊥} Int

(x, y) => x + y
//│ Type: (Int, Int) ->{⊥} Int

((x, y) => x + y)(40, 2)
//│ Type: Int

:e
114 + "514"
//│ ╔══[ERROR] Type error in string literal with expected type Int
//│ ║  l.43: 	114 + "514"
//│ ║        	      ^^^^^
//│ ╙── because: cannot constrain  Str  <:  Int
//│ Type: Int


inc(42)
//│ Type: Int

let x = 42 in x
//│ Type: Int

let x = 1 in let y = 2 in x + y
//│ Type: Int

:e
let x = 0 in x + "1"
//│ ╔══[ERROR] Type error in string literal with expected type Int
//│ ║  l.61: 	let x = 0 in x + "1"
//│ ║        	                 ^^^
//│ ╙── because: cannot constrain  Str  <:  Int
//│ Type: Int

class Foo(x: Int)
//│ Type: ⊤

:e
new Nothingness(0)
//│ ╔══[ERROR] Class Nothingness not found.
//│ ║  l.72: 	new Nothingness(0)
//│ ╙──      	    ^^^^^^^^^^^^^^
//│ Type: ⊥

:e
new 42
//│ ╔══[ERROR] Illegal new expression.
//│ ║  l.79: 	new 42
//│ ╙──      	    ^^
//│ Type: ⊥


new Foo(42)
//│ Type: Foo


class Point(x: Num, y: Num, z: Num)
//│ Type: ⊤


new Point(0.0, 0.0, 0.0)
//│ Type: Point

:e
new Foo("1!5!")
//│ ╔══[ERROR] Type error in string literal with expected type Int
//│ ║  l.98: 	new Foo("1!5!")
//│ ║        	        ^^^^^^
//│ ╙── because: cannot constrain  Str  <:  Int
//│ Type: Foo


class Some[A](value: A)
//│ Type: ⊤

new Some(true)
//│ Type: Some[in ⊤ out Bool]

new Some(42)
//│ Type: Some[in ⊤ out Int]

let p = new Point(1.0, 0.0, 0.0) in p.Point#x
//│ Type: Num

let t = new Some(true) in t.Some#value
//│ Type: Bool

:fixme
:pe
42.Some#value
//│ ╔══[LEXICAL ERROR] Expect at least one digit after the  decimal point
//│ ║  l.123: 	42.Some#value
//│ ╙──       	   ^
//│ ╔══[PARSE ERROR] Expected end of input; found identifier instead
//│ ║  l.123: 	42.Some#value
//│ ╙──       	   ^^^^
//│ Type: Num


class Printer[T](f: T -> Str)
//│ Type: ⊤

fun foofoo(x) =
  let t = x + 1 in "foo"
//│ Type: ⊤

new Printer(foofoo)
//│ Type: Printer[in Int out ⊥]

let ip = new Printer(foofoo) in ip.Printer#f(42)
//│ Type: Str

:e
let ip = new Printer(foofoo) in ip.Printer#f("42")
//│ ╔══[ERROR] Type error in string literal with expected type α33_1
//│ ║  l.147: 	let ip = new Printer(foofoo) in ip.Printer#f("42")
//│ ║         	                                             ^^^^
//│ ╟── because: cannot constrain  Str  <:  α33_1
//│ ╟── because: cannot constrain  Str  <:  α31_1
//│ ╟── because: cannot constrain  Str  <:  α24_1
//│ ╙── because: cannot constrain  Str  <:  Int
//│ Type: Str

class TFun[T](f: T -> T)
//│ Type: ⊤

fun inc(x) = x + 1
//│ Type: ⊤

new TFun(inc)
//│ Type: TFun[Int]

let tf = new TFun(inc) in tf.TFun#f(1)
//│ Type: Int

:e
let tf = new TFun(inc) in tf.TFun#f("1")
//│ ╔══[ERROR] Type error in string literal with expected type α45_1
//│ ║  l.170: 	let tf = new TFun(inc) in tf.TFun#f("1")
//│ ║         	                                    ^^^
//│ ╟── because: cannot constrain  Str  <:  α45_1
//│ ╟── because: cannot constrain  Str  <:  α43_1
//│ ╟── because: cannot constrain  Str  <:  α36_1
//│ ╙── because: cannot constrain  Str  <:  Int
//│ Type: Int

class Pair[A, B](fst: A, snd: B)
//│ Type: ⊤

(new Pair(42, true)).Pair#fst
//│ Type: Int

(new Pair(42, true)).Pair#snd
//│ Type: Bool


if 1 < 2 then 1 else 0
//│ Type: Int

if false then 1 else "1"
//│ Type: Int ∨ Str


if 1 is Int then 1 else 0
//│ Type: Int

fun test(x) =
  if x is Int then x + 1 else 0
test
//│ Type: (¬Int ∨ Int) ->{⊥} Int

test(1)
//│ Type: Int


test("1")
//│ Type: Int


fun fact(n) =
  if n > 1 then n * fact(n - 1) else 1
//│ Type: ⊤

fact
//│ Type: Int ->{⊥} Int

fact(1)
//│ Type: Int


fun fact2 = case
  0 then 1
  n then n * fact2(n - 1)
//│ Type: ⊤

fact2
//│ Type: Int ->{⊥} Int

fact2(0)
//│ Type: Int
