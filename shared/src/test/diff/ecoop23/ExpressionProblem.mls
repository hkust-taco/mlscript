:NewDefs


// * Motivating paper example, demonstrating the expression problem solution


class Add<out E>(lhs: E, rhs: E)
class Lit(n: Int)
//│ class Add[E](lhs: E, rhs: E)
//│ class Lit(n: Int)

fun add11 = Add(Lit(1), Lit(2))
//│ fun add11: Add[Lit]


fun eval(e) =
  if e is
    Lit(n) then n
    Add(l, r) then eval(l) + eval(r)
//│ fun eval: forall 'a. 'a -> Int
//│   where
//│     'a <: Add['a] | Lit


mixin EvalBase {
  fun eval(e) =
    if e is
      Lit(n) then n: Int
      Add(l, r) then this.eval(l) + this.eval(r)
}
//│ mixin EvalBase() {
//│   this: {eval: 'a -> Int}
//│   fun eval: (Add['a] | Lit) -> Int
//│ }


module TestLang extends EvalBase
//│ module TestLang {
//│   fun eval: 'a -> Int
//│ }
//│ where
//│   'a <: Add['a] | Lit

TestLang.eval
//│ 'a -> Int
//│   where
//│     'a <: Add['a] | Lit
//│ res
//│     = [Function: eval]

TestLang.eval(add11)
//│ Int
//│ res
//│     = 3


mixin EvalNothing {
  fun eval(e) = e : nothing
}
mixin EvalAddLit {
  fun eval(e) =
    if e is
      Lit(n) then n: Int
      Add(l, r) then this.eval(l) + this.eval(r)
    else super.eval(e)
}
module TestLang extends EvalNothing, EvalAddLit
//│ mixin EvalNothing() {
//│   fun eval: nothing -> nothing
//│ }
//│ mixin EvalAddLit() {
//│   super: {eval: 'a -> 'b}
//│   this: {eval: 'c -> Int}
//│   fun eval: (Add['c] | Lit | Object & 'a & ~#Add & ~#Lit) -> (Int | 'b)
//│ }
//│ module TestLang {
//│   fun eval: 'd -> Int
//│ }
//│ where
//│   'd <: Add['d] | Lit

TestLang.eval
//│ 'a -> Int
//│   where
//│     'a <: Add['a] | Lit
//│ res
//│     = [Function: eval]

TestLang.eval(add11)
//│ Int
//│ res
//│     = 3


class Neg<out A>(expr: A)
//│ class Neg[A](expr: A)

let add2negadd11 = Add(Lit(2), Neg(add11))
//│ let add2negadd11: Add[Lit | Neg[Add[Lit]]]
//│ add2negadd11
//│              = Add {}


mixin EvalNeg {
  fun eval(e) =
    if e is Neg(d) then 0 - this.eval(d)
    else super.eval(e)
}
//│ mixin EvalNeg() {
//│   super: {eval: 'a -> 'b}
//│   this: {eval: 'c -> Int}
//│   fun eval: (Neg['c] | Object & 'a & ~#Neg) -> (Int | 'b)
//│ }


module TestLang extends EvalBase, EvalNeg
//│ module TestLang {
//│   fun eval: 'a -> Int
//│ }
//│ where
//│   'a <: Neg['a] | Object & (Add['a] | Lit) & ~#Neg

TestLang.eval
//│ 'a -> Int
//│   where
//│     'a <: Neg['a] | Object & (Add['a] | Lit) & ~#Neg
//│ res
//│     = [Function: eval]


TestLang.eval(add11)
//│ Int
//│ res
//│     = 3

TestLang.eval(Neg(add11))
//│ Int
//│ res
//│     = -3

TestLang.eval(Add(Lit(2), Neg(Lit(1))))
//│ Int
//│ res
//│     = 1

TestLang.eval(Neg(Neg(add11)))
//│ Int
//│ res
//│     = 3


TestLang.eval(add2negadd11)
//│ Int
//│ res
//│     = -1

// add11

TestLang.eval(Add(Lit(2), Neg(add11)))
//│ Int
//│ res
//│     = -1


mixin EvalNegNeg_0 {
  fun eval(e) =
    if e is Neg(Neg(d)) then this.eval(d)
    else super.eval(e)
    // * Note: the above is equivalent to:
    // if e is Neg(f) then
    //   if f is Neg(d) then this.eval(d)
    //   else super.eval(e)
    // else super.eval(e)
}
//│ mixin EvalNegNeg_0() {
//│   super: {eval: (Neg[nothing] | 'a) -> 'b}
//│   this: {eval: 'c -> 'b}
//│   fun eval: (Neg[Neg['c] | Object & ~#Neg] | Object & 'a & ~#Neg) -> 'b
//│ }

// * Concise alternative, usign syntax sugar:
mixin EvalNegNeg {
  fun eval(override Neg(Neg(d))) = this.eval(d)
}
//│ mixin EvalNegNeg() {
//│   super: {eval: (Neg[nothing] | 'a) -> 'b}
//│   this: {eval: 'c -> 'b}
//│   fun eval: (Neg[Neg['c] | Object & ~#Neg] | Object & 'a & ~#Neg) -> 'b
//│ }

module TestLang extends EvalBase, EvalNeg, EvalNegNeg
//│ module TestLang {
//│   fun eval: (Neg['A] | Object & 'a & ~#Neg) -> Int
//│ }
//│ where
//│   'A <: 'b & (Neg['b] | Object & ~#Neg)
//│   'b <: Neg['A] | Object & 'a & ~#Neg
//│   'a <: Add['b] | Lit | Neg['b]

fun mk(n) = if n is
  0 then Lit(0)
  1 then Neg(mk(n))
  _ then Add(mk(n), mk(n))
//│ fun mk: forall 'a. Object -> (Lit | 'a)
//│   where
//│     'a :> Add[Lit | 'a] | Neg[Lit | 'a]

TestLang.eval
//│ (Neg['A] | Object & 'a & ~#Neg) -> Int
//│   where
//│     'A <: 'b & (Neg['b] | Object & ~#Neg)
//│     'b <: Neg['A] | Object & 'a & ~#Neg
//│     'a <: Add['b] | Lit | Neg['b]
//│ res
//│     = [Function: eval]

TestLang.eval(mk(0))
//│ Int
//│ res
//│     = 0


