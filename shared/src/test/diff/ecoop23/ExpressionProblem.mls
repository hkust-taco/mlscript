:NewDefs


// * Motivating paper example, demonstrating the expression problem solution


class Add<E>(lhs: E, rhs: E)
class Lit(n: Int)
//│ class Add[E](lhs: E, rhs: E)
//│ class Lit(n: Int)

fun add11 = Add(Lit(1), Lit(2))
//│ fun add11: Add[Lit]


fun eval(e) =
  if e is
    Lit(n) then n
    Add(l, r) then eval(l) + eval(r)
//│ fun eval: forall 'a. 'a -> Int
//│   where
//│     'a <: Add['a] | Lit


mixin EvalBase {
  fun eval(e) =
    if e is
      Lit(n) then n: Int
      Add(l, r) then this.eval(l) + this.eval(r)
}
//│ mixin EvalBase() {
//│   this: {eval: 'lhs -> Int}
//│   fun eval: (Add['lhs] | Lit) -> Int
//│ }


module TestLang extends EvalBase
//│ module TestLang {
//│   fun eval: (Add['E] | Lit) -> Int
//│ }
//│ where
//│   'E <: Add['E] | Lit

TestLang.eval
//│ (Add['E] | Lit) -> Int
//│   where
//│     'E <: Add['E] | Lit
//│ res
//│     = [Function: eval]

TestLang.eval(add11)
//│ Int
//│ res
//│     = 3


mixin EvalNothing {
  fun eval(e) = e : nothing
}
mixin EvalAddLit {
  fun eval(e) =
    if e is
      Lit(n) then n: Int
      Add(l, r) then this.eval(l) + this.eval(r)
    else super.eval(e)
}
module TestLang extends EvalNothing, EvalAddLit
//│ mixin EvalNothing() {
//│   fun eval: nothing -> nothing
//│ }
//│ mixin EvalAddLit() {
//│   super: {eval: 'a -> 'b}
//│   this: {eval: 'lhs -> Int}
//│   fun eval: (Add['lhs] | Lit | Object & 'a & ~#Add & ~#Lit) -> (Int | 'b)
//│ }
//│ module TestLang {
//│   fun eval: (Add['E] | Lit) -> Int
//│ }
//│ where
//│   'E <: Add['E] | Lit

TestLang.eval
//│ (Add['E] | Lit) -> Int
//│   where
//│     'E <: Add['E] | Lit
//│ res
//│     = [Function: eval]

TestLang.eval(add11)
//│ Int
//│ res
//│     = 3


class Neg<A>(expr: A)
//│ class Neg[A](expr: A)

let add2negadd11 = Add(Lit(2), Neg(add11))
//│ let add2negadd11: Add[Lit | Neg[Add[Lit]]]
//│ add2negadd11
//│              = Add {}


mixin EvalNeg {
  fun eval(e) =
    if e is Neg(d) then 0 - this.eval(d)
    else super.eval(e)
}
//│ mixin EvalNeg() {
//│   super: {eval: 'a -> 'b}
//│   this: {eval: 'expr -> Int}
//│   fun eval: (Neg['expr] | Object & 'a & ~#Neg) -> (Int | 'b)
//│ }


module TestLang extends EvalBase, EvalNeg
//│ module TestLang {
//│   fun eval: (Neg['a] | Object & 'b & ~#Neg) -> Int
//│ }
//│ where
//│   'a <: Neg['a] | Object & 'b & ~#Neg
//│   'b <: Add['a] | Lit

TestLang.eval
//│ (Neg['a] | Object & 'b & ~#Neg) -> Int
//│   where
//│     'a <: Neg['a] | Object & 'b & ~#Neg
//│     'b <: Add['a] | Lit
//│ res
//│     = [Function: eval]


TestLang.eval(add11)
//│ Int
//│ res
//│     = 3

TestLang.eval(Neg(add11))
//│ Int
//│ res
//│     = -3

TestLang.eval(Add(Lit(2), Neg(Lit(1))))
//│ Int
//│ res
//│     = 1

TestLang.eval(Neg(Neg(add11)))
//│ Int
//│ res
//│     = 3


TestLang.eval(add2negadd11)
//│ Int
//│ res
//│     = -1

// add11

TestLang.eval(Add(Lit(2), Neg(add11)))
//│ Int
//│ res
//│     = -1


mixin EvalNegNeg_0 {
  fun eval(e) =
    if e is Neg(Neg(d)) then this.eval(d)
    else super.eval(e)
    // * Note: the above is equivalent to:
    // if e is Neg(f) then
    //   if f is Neg(d) then this.eval(d)
    //   else super.eval(e)
    // else super.eval(e)
}
//│ mixin EvalNegNeg_0() {
//│   super: {eval: (Neg[nothing] | 'a) -> 'b}
//│   this: {eval: 'expr -> 'b}
//│   fun eval: (Neg[Neg['expr] | Object & ~#Neg] | Object & 'a & ~#Neg) -> 'b
//│ }

// * Concise alternative, usign syntax sugar:
mixin EvalNegNeg {
  fun eval(override Neg(Neg(d))) = this.eval(d)
}
//│ mixin EvalNegNeg() {
//│   super: {eval: (Neg[nothing] | 'a) -> 'b}
//│   this: {eval: 'expr -> 'b}
//│   fun eval: (Neg[Neg['expr] | Object & ~#Neg] | Object & 'a & ~#Neg) -> 'b
//│ }

module TestLang extends EvalBase, EvalNeg, EvalNegNeg
//│ module TestLang {
//│   fun eval: 'a -> Int
//│ }
//│ where
//│   'a <: Neg['A] | Object & 'b & ~#Neg
//│   'A <: Neg['a & 'A] | Neg['A] & ~#Neg | Object & 'b & ~#Neg
//│   'b <: Add['a] | Lit | Neg['a]

fun mk(n) = if n is
  0 then Lit(0)
  1 then Neg(mk(n))
  _ then Add(mk(n), mk(n))
//│ fun mk: forall 'E. Object -> 'E
//│   where
//│     'E :> Add['E] | Lit | Neg['E]

TestLang.eval
//│ 'a -> Int
//│   where
//│     'a <: Neg['A] | Object & 'b & ~#Neg
//│     'A <: Neg['a & 'A] | Neg['A] & ~#Neg | Object & 'b & ~#Neg
//│     'b <: Add['a] | Lit | Neg['a]
//│ res
//│     = [Function: eval]

TestLang.eval(mk(0))
//│ Int
//│ res
//│     = 0


