:NewDefs


// * Adapted example from Code reuse through polymorphic variants (FOSE 2000)


class Cons[out A](head: A, tail: Cons[A] | Nil)
module Nil
//│ class Cons[A](head: A, tail: Cons[A] | Nil)
//│ module Nil

let l = Cons(1, Nil)
//│ let l: Cons[1]
//│ l
//│   = Cons {}

class NotFound()
class Success[out A](result: A)
//│ class NotFound()
//│ class Success[A](result: A)

fun list_assoc(s, l) =
  if l is
    Cons(h, t) then
      if s === h._1 then Success(h._2)
      else list_assoc(s, t)
    Nil then NotFound()
//│ fun list_assoc: forall 'a 'A. (Eql['a], Cons[{_1: 'a, _2: 'A}] | Nil) -> (NotFound | Success['A])

// fun list_assoc(s: Str, l: Cons[{ _1: Str, _2: 'b }] | Nil): NotFound | Success['b]

class Var(s: Str)
//│ class Var(s: Str)

mixin EvalVar {
  fun eval(sub, v) =
    if v is Var(s) then
      if list_assoc(s, sub) is
        NotFound then v
        Success(r) then r
}
//│ mixin EvalVar() {
//│   fun eval: (Cons[{_1: anything, _2: 'a}] | Nil, Var) -> (Var | 'a)
//│ }

class Abs[A](x: Str, t: A)
class App[A](s: A, t: A)
//│ class Abs[A](x: Str, t: A)
//│ class App[A](s: A, t: A)

fun gensym(): Str = "fun"
//│ fun gensym: () -> Str

fun int_to_string(x: Int): Str = "0"
//│ fun int_to_string: (x: Int) -> Str

mixin EvalLambda {
  fun eval(sub, v) =
    if v is
      App(t1, t2) then
        let l1 = this.eval(sub, t1)
        let l2 = this.eval(sub, t2)
        if t1 is
          Abs(x, t) then this.eval(Cons([x, l2], Nil), t)
        else
          App(l1, l2)
      Abs(x, t) then
        let s = gensym()
        Abs(s, this.eval(Cons([x, Var(s)], sub), t))
    else
      super.eval(sub, v)
}
//│ mixin EvalLambda() {
//│   super: {eval: ('a, 'b) ->{'d} 'c}
//│   this: {
//│     eval: ('a, 'e) ->{'d} ('A & 'f) & (Cons[[Str, 'f]], 'g) ->{'d} 'c & (Cons[[Str, Var] | 'A0], 'h) ->{'d} 'A1
//│   }
//│   fun eval: ('a & (Cons['A0] | Nil), Abs['h] | App['e & (Abs['g] | Object & ~#Abs)] | Object & 'b & ~#Abs & ~#App) ->{'d} (Abs['A1] | App['A] | 'c)
//│ }

module Test1 extends EvalVar, EvalLambda
//│ module Test1 {
//│   fun eval: (Cons[{_1: anything, _2: 'a}] | Nil, Abs['b] | App['c] | Var) -> 'a
//│ }
//│ where
//│   'b <: Abs['b] | App['c] | Var
//│   'c <: 'b & (Abs['b] | Object & ~#Abs)
//│   'a :> Var | App['A] | Abs['A0]
//│   'A0 :> 'a
//│   'A :> 'a

Test1.eval(Nil, Var("a"))
//│ 'a
//│   where
//│     'a :> App['A] | Abs['A0] | Var
//│     'A0 :> 'a
//│     'A :> 'a
//│ res
//│     = Var {}

Test1.eval(Nil, Abs("b", Var("a")))
//│ 'a
//│   where
//│     'a :> App['A] | Abs['A0] | Var
//│     'A0 :> 'a
//│     'A :> 'a
//│ res
//│     = Abs {}

Test1.eval(Cons(["c", Var("d")], Nil), App(Abs("b", Var("b")), Var("c")))
//│ 'a
//│   where
//│     'a :> App['A] | Abs['A0] | Var
//│     'A0 :> 'a
//│     'A :> 'a
//│ res
//│     = Var {}

Test1.eval(Cons(["c", Abs("d", Var("d"))], Nil), App(Abs("b", Var("b")), Var("c")))
//│ 'a
//│   where
//│     'a :> App['A] | Abs['A0] | Abs[Var] | Var
//│     'A0 :> 'a
//│     'A :> 'a
//│ res
//│     = Var {}

class Numb(n: Int)
class Add[A](l: A, r: A)
class Mul[A](l: A, r: A)
//│ class Numb(n: Int)
//│ class Add[A](l: A, r: A)
//│ class Mul[A](l: A, r: A)

fun map_expr(f, v) =
  if v is
    Var then v
    Numb then v
    Add(l, r) then Add(f(l), f(r))
    Mul(l, r) then Mul(f(l), f(r))
//│ fun map_expr: forall 'a 'A 'b 'A0 'c. ('c ->{'b} 'A0 & 'a ->{'b} 'A, Add['c] | Mul['a] | Numb | Var) ->{'b} (Add['A0] | Mul['A] | Numb | Var)

mixin EvalExpr {
  fun eval(sub, v) =
    let eta(e) = this.eval(sub, e)
    let vv = map_expr(eta, v)
    if vv is
      Var then super.eval(sub, vv)
      Add(Numb(l), Numb(r)) then Numb(l + r)
      Mul(Numb(l), Numb(r)) then Numb(l * r)
    else v
}
//│ mixin EvalExpr() {
//│   super: {eval: ('a, Var) ->{'c} 'b}
//│   this: {eval: ('a, 'd) ->{'c} Object}
//│   fun eval: ('a, 'b & (Add['d] | Mul['d] | Numb | Var)) ->{'c} (Numb | 'b)
//│ }

module Test2 extends EvalVar, EvalExpr
//│ module Test2 {
//│   fun eval: forall 'a. (Cons[{_1: anything, _2: Object & 'b}] | Nil, 'a & (Add['c] | Mul['c] | Numb | Var)) -> (Numb | Var | 'a | 'c | 'b)
//│ }
//│ where
//│   'c <: Add['c] | Mul['c] | Numb | Var

Test2.eval(Nil, Var("a"))
//│ Numb | Var
//│ res
//│     = Var {}

Test2.eval(Cons(["c", Abs("d", Var("d"))], Nil), Var("a"))
//│ Abs[Var] | Numb | Var
//│ res
//│     = Var {}

Test2.eval(Cons(["a", Numb(1)], Nil), Var("a"))
//│ Numb | Var
//│ res
//│     = Var {}

// * This expected error shows that Test2 does not handle Abs expression inputs
:e
Test2.eval(Cons(["c", Abs("d", Var("d"))], Nil), Abs("a", Var("a")))
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.183: 	Test2.eval(Cons(["c", Abs("d", Var("d"))], Nil), Abs("a", Var("a")))
//│ ║         	^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── application of type `Abs[?A]` does not match type `Add[?A0] | Mul[?A1] | Numb | Var`
//│ ║  l.183: 	Test2.eval(Cons(["c", Abs("d", Var("d"))], Nil), Abs("a", Var("a")))
//│ ║         	                                                 ^^^^^^^^^^^^^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.136: 	  if v is
//│ ║         	     ^
//│ ╟── from reference:
//│ ║  l.146: 	    let vv = map_expr(eta, v)
//│ ╙──       	                           ^
//│ Abs[Var] | Numb | Var | error
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

Test2.eval(Cons(["a", Abs("d", Var("d"))], Nil), Add(Numb(1), Var("a")))
//│ Abs[Var] | Add[Numb | Var] | Numb | Var
//│ res
//│     = Add {}

module Test3 extends EvalVar, EvalExpr, EvalLambda
//│ module Test3 {
//│   fun eval: (Cons[{_1: anything, _2: 'a}] | Nil, Abs['b] | App['c] | Object & 'd & ~#Abs & ~#App) -> 'e
//│ }
//│ where
//│   'a :> 'e
//│      <: Object
//│   'e :> App['A] | Abs['A0] | Numb | Var | 'd | 'a
//│   'd <: Add['b] | Mul['b] | Numb | Var
//│   'b <: Abs['b] | App['c] | Object & 'd & ~#Abs & ~#App
//│   'c <: 'b & (Abs['b] | Object & ~#Abs)
//│   'A0 :> 'e
//│   'A :> 'e

Test3.eval(Cons(["c", Abs("d", Var("d"))], Nil), Abs("a", Var("a")))
//│ 'a
//│   where
//│     'a :> App['A] | Abs['A0] | Abs[Var] | Numb | Var
//│     'A0 :> 'a
//│     'A :> 'a
//│ res
//│     = Abs {}

Test3.eval(Cons(["c", Abs("d", Var("d"))], Nil), App(Abs("a", Var("a")), Add(Numb(1), Var("c"))))
//│ 'a
//│   where
//│     'a :> Abs['A] | Abs[Var] | Add[Numb | Var] | Numb | Var | App['A0]
//│     'A0 :> 'a
//│     'A :> 'a
//│ res
//│     = Var {}

// * Incorrect version, for regression testing – EvalLambda should be mixed in after EvalExpr
module Test3 extends EvalVar, EvalLambda, EvalExpr
//│ module Test3 {
//│   fun eval: (Cons[{_1: anything, _2: 'a}] | Nil, 'a & (Add['b] | Mul['b] | Numb | Var)) -> ('b | 'c | 'a)
//│ }
//│ where
//│   'a :> 'c | 'b
//│      <: Object
//│   'b <: Add['b] | Mul['b] | Numb | Var
//│   'c :> Abs[Numb | 'c | 'a] | App[Numb | 'c | 'a] | Numb | Var | 'a

// * Because EvalExpr does not dispatch lambdas to super and map_expr only
// * handles exprs
:e
Test3.eval(Cons(["c", Abs("d", Var("d"))], Nil), Abs("a", Var("a")))
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.252: 	Test3.eval(Cons(["c", Abs("d", Var("d"))], Nil), Abs("a", Var("a")))
//│ ║         	^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── application of type `Abs[?A]` does not match type `Add[?A0] | Mul[?A1] | Numb | Var`
//│ ║  l.252: 	Test3.eval(Cons(["c", Abs("d", Var("d"))], Nil), Abs("a", Var("a")))
//│ ║         	                                                 ^^^^^^^^^^^^^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.136: 	  if v is
//│ ║         	     ^
//│ ╟── from reference:
//│ ║  l.146: 	    let vv = map_expr(eta, v)
//│ ╙──       	                           ^
//│ Abs[Var] | error | 'a
//│   where
//│     'a :> Abs[Abs[Var] | Numb | 'a] | App[Abs[Var] | Numb | 'a] | Numb | Var
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

