:NewDefs


// * Adapted example from Code reuse through polymorphic variants (FOSE 2000)


class Cons[out A](head: A, tail: Cons[A] | Nil)
module Nil
//│ class Cons[A](head: A, tail: Cons[A] | Nil)
//│ module Nil

let l = Cons(1, Nil)
//│ let l: Cons[1]
//│ l
//│   = Cons {}

class NotFound()
class Success[out A](result: A)
//│ class NotFound()
//│ class Success[A](result: A)

fun list_assoc(s, l) =
  if l is
    Cons(h, t) then
      if s === h._1 then Success(h._2)
      else list_assoc(s, t)
    Nil then NotFound()
//│ fun list_assoc: forall 'a 'A. (Eql['a], Cons[{_1: 'a, _2: 'A}] | Nil,) -> (NotFound | Success['A])

// fun list_assoc(s: string, l: Cons[{ _1: string, _2: 'b }] | Nil): NotFound | Success['b]

class Var(s: string)
//│ class Var(s: string)

mixin EvalVar {
  fun eval(sub, v) =
    if v is Var(s) then
      if list_assoc(s, sub) is
        NotFound then v
        Success(r) then r
}
//│ mixin EvalVar() {
//│   fun eval: (Cons[{_1: string, _2: 'A}] | Nil, Var,) -> ('A | Var)
//│ }

class Abs[A](x: string, t: A)
class App[A](s: A, t: A)
//│ class Abs[A](x: string, t: A)
//│ class App[A](s: A, t: A)

fun gensym(): string = "fun"
//│ fun gensym: () -> string

fun int_to_string(x: int): string = "0"
//│ fun int_to_string: (x: int,) -> string

mixin EvalLambda {
  fun eval(sub, v) =
    if v is
      App(t1, t2) then
        let l1 = this.eval(sub, t1)
        let l2 = this.eval(sub, t2)
        if t1 is
          Abs(x, t) then this.eval(Cons((x, l2), Nil), t)
        else
          App(l1, l2)
      Abs(x, t) then
        let s = gensym()
        Abs(s, this.eval(Cons((x, Var(s)), sub), t))
    else
      super.eval(sub, v)
}
//│ mixin EvalLambda() {
//│   super: {eval: ('a, 'b,) -> 'c}
//│   this: {eval: ('a, 's,) -> ('A & 'd) & (Cons[(string, 'd,)], 't,) -> 'c & (Cons[(string, Var,) | 'A0], 't0,) -> 'A1}
//│   fun eval: ('a & (Cons['A0] | Nil), Abs['t0] | App['s & (Abs['t] | ~#Abs)] | 'b & ~#Abs & ~#App,) -> (Abs['A1] | App['A] | 'c)
//│ }

module Test1 extends EvalVar, EvalLambda
//│ module Test1 {
//│   fun eval: forall 'a. (Cons[{_1: string, _2: 'A}] | Nil, Abs['t] | App['A0] | Var,) -> ('b | 'a)
//│ }
//│ where
//│   't <: Abs['t] | App['A0] | Var
//│   'A0 <: Abs['t & (Abs['t] | App['A0] | Var)] | Abs['t] & ~#Abs | App['A0] | Var
//│   'A :> 'b | 'a
//│   'b :> 'A | Var | 'a
//│   'a :> App['b | 'a] | Abs['b | 'a]

Test1.eval(Nil, Var("a"))
//│ forall 'a. 'b | 'a | 'c
//│   where
//│     'b :> forall 'a. 'a | 'c
//│     'a :> App[forall 'a. 'b | 'a | 'c] | Abs[forall 'a. 'b | 'a | 'c]
//│     'c :> (forall 'a. Var | 'b | 'a) | Var
//│ res
//│     = Var {}

Test1.eval(Nil, Abs("b", Var("a")))
//│ forall 'a. 'b | 'a | 'c
//│   where
//│     'b :> forall 'a. 'a | 'c
//│     'a :> App[forall 'a. 'b | 'a | 'c] | Abs[forall 'a. 'b | 'a | 'c]
//│     'c :> (forall 'a. Var | 'b | 'a) | Var
//│ res
//│     = Abs {}

Test1.eval(Cons(("c", Var("d")), Nil), App(Abs("b", Var("b")), Var("c")))
//│ forall 'a. 'b | 'a | 'c
//│   where
//│     'b :> forall 'a. 'a | 'c
//│     'a :> App[forall 'a. 'b | 'a | 'c] | Abs[forall 'a. 'b | 'a | 'c]
//│     'c :> (forall 'a. Var | 'b | 'a) | Var
//│ res
//│     = Var {}

Test1.eval(Cons(("c", Abs("d", Var("d"))), Nil), App(Abs("b", Var("b")), Var("c")))
//│ forall 'a. 'b | 'a | 'c
//│   where
//│     'b :> forall 'a. 'a | 'c
//│     'a :> App[forall 'a. 'b | 'a | 'c] | Abs[forall 'a. 'b | 'a | 'c]
//│     'c :> (forall 'a. Abs[Var] | Var | 'b | 'a) | Var
//│ res
//│     = Var {}

class Num(n: int)
class Add[A](l: A, r: A)
class Mul[A](l: A, r: A)
//│ class Num(n: int)
//│ class Add[A](l: A, r: A)
//│ class Mul[A](l: A, r: A)

fun map_expr(f, v) =
  if v is
    Var then v
    Num then v
    Add(l, r) then Add(f(l), f(r))
    Mul(l, r) then Mul(f(l), f(r))
//│ fun map_expr: forall 'l 'A 'l0 'A0. ('l -> 'A & 'l0 -> 'A0, Add['l] | Mul['l0] | Num | Var,) -> (Add['A] | Mul['A0] | Num | Var)

mixin EvalExpr {
  fun eval(sub, v) =
    fun eta(e) = this.eval(sub, e)
    let vv = map_expr(eta, v)
    if vv is
      Var then super.eval(sub, vv)
      Add(Num(l), Num(r)) then Num(l + r)
      Mul(Num(l), Num(r)) then Num(l * r)
    else v
}
//│ mixin EvalExpr() {
//│   super: {eval: ('a, Var,) -> 'b}
//│   this: {eval: ('a, 'c,) -> anything}
//│   fun eval: ('a, 'b & (Add['c] | Mul['c] | Num | Var),) -> (Num | 'b)
//│ }

module Test2 extends EvalVar, EvalExpr
//│ module Test2 {
//│   fun eval: forall 'a. (Cons[{_1: string, _2: 'A}] | Nil, 'a & (Add['b] | Mul['b] | Num | Var),) -> ('A | Num | Var | 'a)
//│ }
//│ where
//│   'b <: Add['b] | Mul['b] | Num | Var

Test2.eval(Nil, Var("a"))
//│ Num | Var
//│ res
//│     = Var {}

Test2.eval(Cons(("c", Abs("d", Var("d"))), Nil), Var("a"))
//│ Abs[Var] | Num | Var
//│ res
//│     = Var {}

Test2.eval(Cons(("a", Num(1)), Nil), Var("a"))
//│ Num | Var
//│ res
//│     = Var {}

// * This expected error shows that Test2 does not handle Abs expression inputs
:e
Test2.eval(Cons(("c", Abs("d", Var("d"))), Nil), Abs("a", Var("a")))
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.181: 	Test2.eval(Cons(("c", Abs("d", Var("d"))), Nil), Abs("a", Var("a")))
//│ ║         	^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── application of type `Abs[?A]` does not match type `Add[?A0] | Mul[?A1] | Num | Var`
//│ ║  l.181: 	Test2.eval(Cons(("c", Abs("d", Var("d"))), Nil), Abs("a", Var("a")))
//│ ║         	                                                 ^^^^^^^^^^^^^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.134: 	  if v is
//│ ║         	     ^
//│ ╟── from reference:
//│ ║  l.144: 	    let vv = map_expr(eta, v)
//│ ╙──       	                           ^
//│ Abs[Var] | Num | Var | error
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

Test2.eval(Cons(("a", Abs("d", Var("d"))), Nil), Add(Num(1), Var("a")))
//│ Abs[Var] | Add[Num | Var] | Num | Var
//│ res
//│     = Add {}

module Test3 extends EvalVar, EvalExpr, EvalLambda
//│ module Test3 {
//│   fun eval: forall 'A 'a. (Cons[{_1: string, _2: 'A0}] | Nil, Abs['t] | App['A] | 'b & ~#Abs & ~#App,) -> ('c | 'a)
//│ }
//│ where
//│   'A0 :> 'c | 'a
//│   'c :> 'A0 | Num | Var | 'b | 'a
//│   'a :> App['c | 'a] | Abs['c | 'a]
//│   'b <: Add['d] | Mul['d] | Num | Var
//│   'd <: Abs['t] | App['A] | 'b & ~#Abs & ~#App
//│   't <: Abs['t] | App['A] | 'b & ~#Abs & ~#App
//│   'A <: Abs['t & (Abs['t] | App['A] | 'b & ~#Abs & ~#App)] | Abs['t] & ~#Abs | App['A] | 'b & ~#Abs & ~#App

Test3.eval(Cons(("c", Abs("d", Var("d"))), Nil), Abs("a", Var("a")))
//│ forall 'a. 'b | 'a | 'c
//│   where
//│     'b :> forall 'a. 'a | 'c
//│     'a :> App[forall 'a. 'b | 'a | 'c] | Abs[forall 'a. 'b | 'a | 'c]
//│     'c :> (forall 'a. Abs[Var] | Var | 'b | 'a) | Num | Var
//│ res
//│     = Abs {}

Test3.eval(Cons(("c", Abs("d", Var("d"))), Nil), App(Abs("a", Var("a")), Add(Num(1), Var("c"))))
//│ forall 'a. 'b | 'a | 'c
//│   where
//│     'b :> forall 'a. 'a | 'c
//│     'a :> App[forall 'a. 'b | 'a | 'c] | Abs[forall 'a. 'b | 'a | 'c]
//│     'c :> (forall 'a. Abs[Var] | Var | 'b | 'a) | Add[Num | Var] | Num | Var
//│ res
//│     = Var {}

// * Incorrect version, for regression testing – EvalLambda should be mixed in after EvalExpr
module Test3 extends EvalVar, EvalLambda, EvalExpr
//│ module Test3 {
//│   fun eval: forall 'a. (Cons[{_1: string, _2: 'A}] | Nil, 'a & (Add['b] | Mul['b] | Num | Var),) -> ('a | 'c)
//│ }
//│ where
//│   'b <: Add['b] | Mul['b] | Num | Var
//│   'A :> 'c
//│   'c :> 'A | Abs[Num | 'c] | App[Num | 'c] | Num | Var

// * Because EvalExpr does not dispatch lambdas to super and map_expr only
// * handles exprs
:e
Test3.eval(Cons(("c", Abs("d", Var("d"))), Nil), Abs("a", Var("a")))
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.248: 	Test3.eval(Cons(("c", Abs("d", Var("d"))), Nil), Abs("a", Var("a")))
//│ ║         	^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── application of type `Abs[?A]` does not match type `Add[?A0] | Mul[?A1] | Num | Var`
//│ ║  l.248: 	Test3.eval(Cons(("c", Abs("d", Var("d"))), Nil), Abs("a", Var("a")))
//│ ║         	                                                 ^^^^^^^^^^^^^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.134: 	  if v is
//│ ║         	     ^
//│ ╟── from reference:
//│ ║  l.144: 	    let vv = map_expr(eta, v)
//│ ╙──       	                           ^
//│ error | 'a
//│   where
//│     'a :> Abs[Var] | Num | Var | Abs['a] | App['a]
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

