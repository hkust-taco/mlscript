:NewParser
:NewDefs


// * Adapted example from Compositional Embeddings of Domain-Specific Languages (OOPSLA 2022)


// ******************* Initial System *******************

class Vector(x: int, y: int)
//│ class Vector(x: int, y: int)

class Circle(radius: int)
class Outside[Region](a: Region)
class Union[Region](a: Region, b: Region)
class Intersect[Region](a: Region, b: Region)
class Translate[Region](v: Vector, a: Region)
//│ class Circle(radius: int)
//│ class Outside[Region](a: Region)
//│ class Union[Region](a: Region, b: Region)
//│ class Intersect[Region](a: Region, b: Region)
//│ class Translate[Region](v: Vector, a: Region)

mixin SizeBase {
  fun size(r) =
    if r is
      Circle(_) then 1
      Outside(a) then this.size(a) + 1
      Union(a, b) then this.size(a) + this.size(b) + 1
      Intersect(a, b) then this.size(a) + this.size(b) + 1
      Translate(_, a) then this.size(a) + 1
}
//│ mixin SizeBase() {
//│   this: {size: ('a | 'a0 | 'a1 | 'a2) -> int}
//│   fun size: (Circle | Intersect['a2] | Outside['a0] | Translate['a] | Union['a1]) -> int
//│ }

// ******************* Linguistic Reuse and Meta-Language Optimizations *******************

fun round(n: number): int = 0
//│ fun round: (n: number,) -> int

fun go(x, offset) =
  if x is 0 then Circle(1)
  else
    let shared = go(x - 1, round(offset / 2))
    Union(Translate(Vector(0 - offset, 0), shared), Translate(Vector(offset, 0), shared))
//│ fun go: forall 'Region. (int, int,) -> 'Region
//│   where
//│     'Region :> Circle | Union[Translate['Region]]

// * Note that first-class polymorphism manages (correctly) to preserve the universal quantification
let circles = go(2, 1024)
//│ let circles: forall 'Region. 'Region
//│   where
//│     'Region :> Circle | Union[Translate['Region]]
//│ circles
//│         = Union {}

// ******************* Adding More Language Constructs *******************

class Univ
class Empty
class Scale[Region](v: Vector, a: Region)
//│ class Univ()
//│ class Empty()
//│ class Scale[Region](v: Vector, a: Region)

mixin SizeExt {
  fun size(a) =
    if a is
      Univ then 1
      Empty then 1
      Scale(_, b) then this.size(b) + 1
    else super.size(a)
}
//│ mixin SizeExt() {
//│   super: {size: 'b -> 'c}
//│   this: {size: 'a -> int}
//│   fun size: (Empty | Scale['a] | Univ | 'b & ~#Empty & ~#Scale & ~#Univ) -> (int | 'c)
//│ }

module TestSize extends SizeBase, SizeExt
//│ module TestSize() {
//│   fun size: 'a -> int
//│ }
//│ where
//│   'a <: Circle | Empty | Intersect['a] | Outside['a] | Scale['a] | Translate['a] | Union['a] | Univ

// TODO investigate
:re
TestSize.size(Empty())
//│ int
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

// TODO investigate
:re
TestSize.size(circles)
//│ int
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

// TODO investigate
:re
TestSize.size(Scale(Vector(1, 1), circles))
//│ int
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

// ******************* Adding a New Interpretation *******************
// a stupid power (int ** int) implementation
fun pow(x, a) =
  if a is 0 then 1
  else x * pow(x, a - 1)
//│ fun pow: (int, int,) -> int

mixin Contains {
  fun contains(a, p) =
    if a is
      Circle(r) then pow(p.x, 2) + pow(p.y, 2) <= pow(r, 2)
      Outside(a) then not (this.contains(a, p))
      Union(lhs, rhs) then this.contains(lhs, p) || this.contains(rhs, p)
      Intersect(lhs, rhs) then this.contains(lhs, p) && this.contains(rhs, p)
      Translate(v, a) then this.contains(a, Vector(p.x - v.x, p.y - v.y))
}
//│ mixin Contains() {
//│   this: {contains: ('a | 'a0 | 'a1, 'b,) -> bool & ('a2, Vector,) -> 'c}
//│   fun contains: (Circle | Intersect['a1] | Outside['a] | Translate['a2] | Union['a0], {x: int, y: int} & 'b,) -> (bool | 'c)
//│ }

module TestContains extends Contains
//│ module TestContains() {
//│   fun contains: ('a, {x: int, y: int},) -> bool
//│ }
//│ where
//│   'a <: Circle | Intersect['a] | Outside['a] | Translate['a] | Union['a]

// TODO investigate
:re
TestContains.contains(Translate(Vector(0, 0), Circle(1)), Vector(0, 0))
//│ bool
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

// TODO investigate
:re
TestContains.contains(Intersect(Translate(Vector(0, 0), Circle(1)), Circle(1)), Vector(0, 0))
//│ bool
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

// TODO investigate
:re
TestContains.contains(circles, Vector(0, 0))
//│ bool
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

// ******************* Dependencies, Complex Interpretations, and Domain-Specific Optimizations *******************

fun toString(a: int): string = "foo"
fun concat(a: string, b: string): string = a
//│ fun toString: (a: int,) -> string
//│ fun concat: (a: string, b: string,) -> string

mixin Text {
  fun text(e) =
    if e is
      Circle(r) then concat("a circular region of radius ", toString(r))
      Outside(a) then concat("outside a region of size ", toString(this.size(a)))
      Union then concat("the union of two regions of size ", toString(this.size(e)))
      Intersect then concat("the intersection of two regions of size ", toString(this.size(e)))
      Translate then concat("a translated region of size ", toString(this.size(e)))
}
//│ mixin Text() {
//│   this: {size: (Intersect['Region] | Translate['Region0] | Union['Region1] | 'a) -> int}
//│   fun text: (Circle | Intersect['Region] | Outside['a] | Translate['Region0] | Union['Region1]) -> string
//│ }

:e
module SizeText extends Text
//│ ╔══[ERROR] Module `SizeText` does not contain member `size`
//│ ║  l.180: 	      Translate then concat("a translated region of size ", toString(this.size(e)))
//│ ╙──       	                                                                         ^^^^^
//│ ╔══[ERROR] Module `SizeText` does not contain member `size`
//│ ║  l.179: 	      Intersect then concat("the intersection of two regions of size ", toString(this.size(e)))
//│ ╙──       	                                                                                     ^^^^^
//│ ╔══[ERROR] Module `SizeText` does not contain member `size`
//│ ║  l.178: 	      Union then concat("the union of two regions of size ", toString(this.size(e)))
//│ ╙──       	                                                                          ^^^^^
//│ ╔══[ERROR] Module `SizeText` does not contain member `size`
//│ ║  l.177: 	      Outside(a) then concat("outside a region of size ", toString(this.size(a)))
//│ ╙──       	                                                                       ^^^^^
//│ module SizeText() {
//│   fun text: (Circle | Intersect[anything] | Outside[anything] | Translate[anything] | Union[anything]) -> string
//│ }

// * Note: this inferred type got *much worse* after this commit (field access type refinement)
module SizeText extends SizeBase, Text
//│ module SizeText() {
//│   fun size: 'a -> int
//│   fun text: (Circle | Intersect[Circle | Intersect['Region] | Outside['Region0] | Translate['Region1] | Union['Region2]] | Outside['a] | Translate[Circle | Intersect['Region] | Outside['Region0] | Translate['Region1] | Union['Region2]] | Union[Circle | Intersect['Region] | Outside['Region0] | Translate['Region1] | Union['Region2]]) -> string
//│ }
//│ where
//│   'a <: Circle | Intersect['Region] | Outside['Region0] | Translate['Region1] | Union['Region2]
//│   'Region2 <: 'a
//│   'Region1 <: 'a
//│   'Region0 <: 'a
//│   'Region <: 'a

SizeText.text(circles)
//│ string
//│ res
//│     = 'the union of two regions of size '

SizeText.size(circles)
//│ int
//│ res
//│     = 13

SizeText.text(Intersect(Translate(Vector(0, 0), Circle(1)), Circle(1)))
//│ string
//│ res
//│     = 'the intersection of two regions of size '

SizeText.size(Intersect(Translate(Vector(0, 0), Circle(1)), Circle(1)))
//│ int
//│ res
//│     = 4

mixin IsUniv {
  fun isUniv(e) =
    if e is
      Univ then true
      Outside(a) then this.isEmpty(a)
      Union(a, b) then this.isUniv(a) || this.isUniv(b)
      Intersect(a, b) then this.isUniv(a) && this.isUniv(b)
      Translate(_, a) then this.isUniv(a)
      Scale(_, a) then this.isUniv(a)
    else false
}
//│ mixin IsUniv() {
//│   this: {isEmpty: 'a -> 'b, isUniv: ('a0 | 'a1) -> bool & ('a2 | 'a3) -> 'b}
//│   fun isUniv: (Intersect['a1] | Outside['a] | Scale['a3] | Translate['a2] | Union['a0] | Univ | ~Intersect[anything] & ~Outside[anything] & ~Scale[anything] & ~Translate[anything] & ~Union[anything] & ~Univ) -> (bool | 'b)
//│ }

mixin IsEmpty {
  fun isEmpty(e) =
    if e is
      Univ then true
      Outside(a) then this.isUniv(a)
      Union(a, b) then this.isEmpty(a) || this.isEmpty(b)
      Intersect(a, b) then this.isEmpty(a) && this.isEmpty(b)
      Translate(_, a) then this.isEmpty(a)
      Scale(_, a) then this.isEmpty(a)
    else false
}
//│ mixin IsEmpty() {
//│   this: {isEmpty: ('a | 'a0) -> bool & ('a1 | 'a2) -> 'b, isUniv: 'a3 -> 'b}
//│   fun isEmpty: (Intersect['a0] | Outside['a3] | Scale['a2] | Translate['a1] | Union['a] | Univ | ~Intersect[anything] & ~Outside[anything] & ~Scale[anything] & ~Translate[anything] & ~Union[anything] & ~Univ) -> (bool | 'b)
//│ }

module IsUnivIsEmpty extends IsUniv, IsEmpty
//│ module IsUnivIsEmpty() {
//│   fun isEmpty: 'a -> bool
//│   fun isUniv: 'b -> bool
//│ }
//│ where
//│   'a <: Intersect['a] | Outside['b] | Scale['a] | Translate['a] | Union['a] | Univ | ~Intersect[anything] & ~Outside[anything] & ~Scale[anything] & ~Translate[anything] & ~Union[anything] & ~Univ
//│   'b <: Intersect['b] | Outside['a] | Scale['b] | Translate['b] | Union['b] | Univ | ~Intersect[anything] & ~Outside[anything] & ~Scale[anything] & ~Translate[anything] & ~Union[anything] & ~Univ

module IsUnivIsEmpty extends IsEmpty, IsUniv
//│ module IsUnivIsEmpty() {
//│   fun isEmpty: 'a -> bool
//│   fun isUniv: 'b -> bool
//│ }
//│ where
//│   'a <: Intersect['a] | Outside['b] | Scale['a] | Translate['a] | Union['a] | Univ | ~Intersect[anything] & ~Outside[anything] & ~Scale[anything] & ~Translate[anything] & ~Union[anything] & ~Univ
//│   'b <: Intersect['b] | Outside['a] | Scale['b] | Translate['b] | Union['b] | Univ | ~Intersect[anything] & ~Outside[anything] & ~Scale[anything] & ~Translate[anything] & ~Union[anything] & ~Univ

IsUnivIsEmpty.isUniv(circles)
//│ bool
//│ res
//│     = false

IsUnivIsEmpty.isEmpty(circles)
//│ bool
//│ res
//│     = false

class Foo
IsUnivIsEmpty.isEmpty(Scale(Vector(1, 2), Intersect(Foo(), circles)))
//│ class Foo()
//│ bool
//│ res
//│     = false

mixin Eliminate {
  fun eliminate(e) =
    if e is
      Outside(Outside(a)) then this.eliminate(a)
      Outside(a) then Outside(this.eliminate(a))
      Union(a, b) then Union(this.eliminate(a), this.eliminate(b))
      Intersect(a, b) then Intersect(this.eliminate(a), this.eliminate(b))
      Translate(v, a) then Translate(v, this.eliminate(a))
      Scale(v, a) then Scale(v, this.eliminate(a))
    else e
}
//│ mixin Eliminate() {
//│   this: {eliminate: 'a -> 'b & 'a0 -> 'c & 'a1 -> 'd & 'a2 -> 'e & 'a3 -> 'f & 'a4 -> 'g}
//│   fun eliminate: forall 'h. (Intersect['a2] | Outside['a0 & (Outside['a] | ~#Outside)] | Scale['a4] | Translate['a3] | Union['a1] | 'h & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union) -> (Intersect['e] | Outside['c] | Scale['g] | Translate['f] | Union['d] | 'b | 'h)
//│ }

module TestElim extends Eliminate
//│ module TestElim() {
//│   fun eliminate: 'a -> 'b
//│ }
//│ where
//│   'a <: Intersect['a] | Outside['a & (Outside['a] | ~#Outside)] | Scale['a] | Translate['a] | Union['a] | 'b & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union
//│   'b :> Outside['b] | Union['b] | Intersect['b] | Translate['b] | Scale['b]

TestElim.eliminate(Outside(Outside(Univ())))
//│ 'a
//│   where
//│     'a :> Intersect['a] | Translate['a] | Scale['a] | Univ | Outside['a] | Union['a]
//│ res
//│     = Univ {}

TestElim.eliminate(circles)
//│ 'a
//│   where
//│     'a :> Outside['a] | Union['a] | Intersect['a] | Translate['a] | Scale['a] | Circle
//│ res
//│     = Union {}

fun mk(n) = if n is
  1 then Outside(mk(n))
  2 then Union(mk(n), mk(n))
  3 then Intersect(mk(n), mk(n))
  4 then Translate(Vector(0, 0), mk(n))
  _ then Scale(Vector(0, 0), mk(n))
//│ fun mk: forall 'Region. number -> 'Region
//│   where
//│     'Region :> Intersect['Region] | Outside['Region] | Scale['Region] | Translate['Region] | Union['Region]

:re
TestElim.eliminate(mk(100))
//│ 'a
//│   where
//│     'a :> Outside['a] | Union['a] | Intersect['a] | Translate['a] | Scale['a]
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

// *************************************************************************

module Lang extends SizeBase, SizeExt, Contains, Text, IsUniv, IsEmpty, Eliminate
//│ module Lang() {
//│   fun contains: ('a, {x: int, y: int},) -> bool
//│   fun eliminate: 'b -> 'c
//│   fun isEmpty: 'd -> bool
//│   fun isUniv: 'e -> bool
//│   fun size: 'f -> int
//│   fun text: (Circle | Intersect[Empty | Scale['Region] | Univ | 'g & ~#Empty & ~#Scale & ~#Univ] | Outside['f] | Translate[Empty | Scale['Region] | Univ | 'g & ~#Empty & ~#Scale & ~#Univ] | Union[Empty | Scale['Region] | Univ | 'g & ~#Empty & ~#Scale & ~#Univ]) -> string
//│ }
//│ where
//│   'f <: Empty | Scale['Region] | Univ | 'g & ~#Empty & ~#Scale & ~#Univ
//│   'g <: Circle | Intersect['f] | Outside['f] | Translate['f] | Union['f]
//│   'Region <: 'f
//│   'd <: Intersect['d] | Outside['e] | Scale['d] | Translate['d] | Union['d] | Univ | ~Intersect[anything] & ~Outside[anything] & ~Scale[anything] & ~Translate[anything] & ~Union[anything] & ~Univ
//│   'e <: Intersect['e] | Outside['d] | Scale['e] | Translate['e] | Union['e] | Univ | ~Intersect[anything] & ~Outside[anything] & ~Scale[anything] & ~Translate[anything] & ~Union[anything] & ~Univ
//│   'b <: Intersect['b] | Outside['b & (Outside['b] | ~#Outside)] | Scale['b] | Translate['b] | Union['b] | 'c & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union
//│   'c :> Translate['c] | Scale['c] | Outside['c] | Union['c] | Intersect['c]
//│   'a <: Circle | Intersect['a] | Outside['a] | Translate['a] | Union['a]

// TODO investigate
:re
Lang.size(circles)
//│ int
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

// TODO investigate
:re
Lang.contains(circles, Vector(0, 0))
//│ bool
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

// TODO investigate
:re
Lang.text(circles)
//│ string
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

Lang.isUniv(circles)
//│ bool
//│ res
//│     = false

Lang.isEmpty(circles)
//│ bool
//│ res
//│     = false

// TODO investigate
:re
Lang.size(Lang.eliminate(circles))
//│ int
//│ res
//│ Runtime error:
//│   Error: non-exhaustive case expression

:re
Lang.size(mk(100))
//│ int
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:e
:re
Lang.contains(mk(100), Vector(0, 0))
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.434: 	Lang.contains(mk(100), Vector(0, 0))
//│ ║         	^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── application of type `Scale[?Region]` does not match type `Circle | Intersect[?Region0] | Outside[?Region1] | Translate[?Region2] | Union[?Region3]`
//│ ║  l.348: 	  _ then Scale(Vector(0, 0), mk(n))
//│ ║         	         ^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── but it flows into application with expected type `Circle | Intersect[?Region4] | Outside[?Region5] | Translate[?Region6] | Union[?Region7]`
//│ ║  l.434: 	Lang.contains(mk(100), Vector(0, 0))
//│ ║         	              ^^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.123: 	    if a is
//│ ╙──       	       ^
//│ error | bool
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:e
:re
Lang.text(mk(100))
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.454: 	Lang.text(mk(100))
//│ ║         	^^^^^^^^^^^^^^^^^^
//│ ╟── application of type `Scale[?Region]` does not match type `Circle | Intersect[?Region0] | Outside[?Region1] | Translate[?Region2] | Union[?Region3]`
//│ ║  l.348: 	  _ then Scale(Vector(0, 0), mk(n))
//│ ║         	         ^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── but it flows into application with expected type `Circle | Intersect[?Region4] | Outside[?Region5] | Translate[?Region6] | Union[?Region7]`
//│ ║  l.454: 	Lang.text(mk(100))
//│ ║         	          ^^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.175: 	    if e is
//│ ╙──       	       ^
//│ error | string
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:re
Lang.isUniv(mk(100))
//│ bool
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:re
Lang.isEmpty(mk(100))
//│ bool
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:re
Lang.size(Lang.eliminate(mk(100)))
//│ int
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded
