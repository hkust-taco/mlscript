:NoRecursiveTypes
:NoJS


// ------------ Dummy classes to represent the types in the examples ------------

class List[a]
  method Get: a
//│ Defined class List[+a]
//│ Declared List.Get: List['a] -> 'a

class ST[S, A]
  method Inv_S: S -> S
  method Cov_A: A
//│ Defined class ST[=S, +A]
//│ Declared ST.Inv_S: ST['S, ?] -> 'S -> 'S
//│ Declared ST.Cov_A: ST['S, 'A] -> 'A


// ============ Type signatures for functions used in the examples ============

def choose x y = if true then x else y
//│ choose: 'a -> 'a -> 'a

def id x = x
//│ id: 'a -> 'a

def auto (x: forall 'a. 'a -> 'a) = x x
//│ auto: (forall 'a. 'a -> 'a) -> 'a0 -> 'a0

def app f x = f x
//│ app: ('a -> 'b) -> 'a -> 'b

def inc: int -> int
//│ inc: int -> int

// Used to represent `::` in the papers
def cons: 'a -> List['a] -> List['a]
//│ cons: 'a -> List['a] -> List['a]

// Used to represent `[]` in the papers
def nil: List['a]
//│ nil: List[nothing]

def single: 'a -> List['a]
//│ single: 'a -> List['a]

def head: List['a] -> 'a
//│ head: List['a] -> 'a

def tail: List['a] -> List['a]
//│ tail: List['a] -> List['a]

def append: List['a] -> List['a] -> List['a]
//│ append: List['a] -> List['a] -> List['a]

def runST: (forall 's. ST['s, 'v]) -> 'v
//│ runST: (forall 's. ST['s, 'v]) -> 'v

def argST: ST['s, int]
//│ argST: ST['s, int]


// ============ Raising ML to the power of System F (2003) ============

// FreezeML A2
choose id
//│ res: 'a -> ('b -> 'b | 'a)

fun (g: forall 'a. ('a -> 'a) -> ('a -> 'a)) -> fun (x: forall 'a. 'a -> 'a) -> fun a -> g a (x a)
//│ res: (forall 'a. ('a -> 'a) -> 'a -> 'a) -> (forall 'a0. 'a0 -> 'a0) -> ('a1 -> 'a1 & 'a1) -> 'a1

choose id succ
//│ res: int -> int

// FreezeML A7
choose id auto
//│ res: (forall 'a. 'a -> 'a & 'b) -> ('a0 -> 'a0 | 'b)

// Not typeable in MLF, along with anything with it as a subterm
// i.e. unannotated auto
omega = fun x -> x x
//│ omega: ('a -> 'b & 'a) -> 'b

fun (x: forall 'a. 'a) -> x x
//│ res: nothing -> nothing

// i.e. auto
omegad = fun (x: forall 'a. 'a -> 'a) -> x x
//│ omegad: (forall 'a. 'a -> 'a) -> 'a0 -> 'a0

// FreezeML A5
id auto
//│ res: (forall 'a. 'a -> 'a) -> 'a0 -> 'a0

// ~ auto id (FreezeML F5, PolyML 1.1)
(fun x -> x id) auto
//│ res: 'a -> 'a

app auto id
//│ res: 'a -> 'a

// ------------ Sec 5.2 ------------

// * Mistake in the paper (confirmed by Rémy via email).
:e
let f = choose id in (f auto) (f succ)
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.107: 	let f = choose id in (f auto) (f succ)
//│ ║         	                     ^^^^^^^^^^^^^^^^^
//│ ╟── type `'a` is not an instance of type `int`
//│ ║  l.28: 	def auto (x: forall 'a. 'a -> 'a) = x x
//│ ║        	                        ^^
//│ ╟── Note: quantified type variable 'a is defined at:
//│ ║  l.28: 	def auto (x: forall 'a. 'a -> 'a) = x x
//│ ╙──      	                    ^^
//│ res: int -> int | error

// * Reproduced with an unnanotated auto:

def auto2 x = x x
//│ auto2: ('a -> 'b & 'a) -> 'b

choose id auto2
//│ res: ('a -> 'b & 'a) -> ('b | 'a)

:e // * This is a legit error
res (choose id succ)
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.128: 	res (choose id succ)
//│ ║         	^^^^^^^^^^^^^^^^^^^^
//│ ╟── reference of type `int -> int` is not an instance of type `int`
//│ ║  l.128: 	res (choose id succ)
//│ ║         	               ^^^^
//│ ╟── but it flows into application with expected type `int`
//│ ║  l.128: 	res (choose id succ)
//│ ╙──       	     ^^^^^^^^^^^^^^
//│ res: int -> int | error | int

// * Didier Le Botlan suggested this fix by email:

let f = choose id in (f auto, f succ)
//│ res: (forall 'b 'a. (forall 'a0. 'a0 -> 'a0 & 'b) -> ('a -> 'a | 'b), forall 'b. (int & 'b) -> (int | 'b),)

let f = choose id in (f auto2, f succ)
//│ res: (forall 'a 'b 'c. ('a -> 'b & 'a & 'c) -> ('b | 'c), forall 'c. (int & 'c) -> (int | 'c),)

// ------------ Sec 6 ------------

// (λ(y) y I ; y K) (λ(x) x x)
// "typable in Fω but not in F [9]"
// [9] P. Giannini and S. R. D. Rocca. Characterization of typings in polymorphic type discipline. In Third annual Symposium on Logic in Computer Science, pages 61–70. IEEE, 1988.

// I := λx.x
I x = x
//│ I: 'a -> 'a

// K := λx.λy.x
K x y = x
//│ K: 'a -> anything -> 'a

(fun y -> let tmp = y I in y K) (fun x -> x x)
//│ res: anything -> 'a -> anything -> 'a

// * Note: reduces to
let tmp = (fun x -> x x) I in (fun x -> x x) K
//│ res: anything -> 'a -> anything -> 'a

// to
let tmp = I I in K K
//│ res: anything -> 'a -> anything -> 'a


// ============ Recasting MLF (2009) ============

// ------------ Sec 1.3 ------------

// ~ FreezeML B1
(fun f -> (f 42, f "foo")) (fun x -> x)
//│ res: (42, "foo",)

(fun f -> (f succ, choose f auto)) (choose id)
//│ res: (int -> int | 'b, (forall 'a. 'a -> 'a & 'b) -> (int -> int | 'b),)

// ------------ Sec 2.3.1 ------------

// * i.e. id auto (FreezeML A5)
(fun z -> z) omegad
(fun z -> z) omega
//│ res: (forall 'a. 'a -> 'a) -> 'a0 -> 'a0
//│ res: ('a -> 'b & 'a) -> 'b

// * i.e. auto id (FreezeML F5)
(fun x -> x x) id
//│ res: 'a -> 'a

fun z -> (z omegad)
fun z -> (z omega)
//│ res: ((forall 'a. (forall 'a0. 'a0 -> 'a0) -> 'a -> 'a) -> 'b) -> 'b
//│ res: ((forall 'a 'b. ('a -> 'b & 'a) -> 'b) -> 'c) -> 'c

(fun y -> fun z -> z y) omegad
(fun y -> fun z -> z y) omega
//│ res: ((forall 'a. (forall 'a0. 'a0 -> 'a0) -> 'a -> 'a) -> 'b) -> 'b
//│ res: ((forall 'a 'b. ('a -> 'b & 'a) -> 'b) -> 'c) -> 'c

fun z -> omegad z
fun z -> omega z
//│ res: (forall 'a. 'a -> 'a) -> 'a0 -> 'a0
//│ res: ('a -> 'b & 'a) -> 'b

(fun x -> fun y -> x y) omegad
(fun x -> fun y -> x y) omega
//│ res: (forall 'a. 'a -> 'a) -> (forall 'a0. 'a0 -> 'a0)
//│ res: ('a -> 'b & 'a) -> 'b

// ------------ Sec 4.3 ------------
// affects the order of bindings in the result type in MLF, not very interesting otherwise

fun (x: forall 'a. 'a -> 'b -> 'a) -> x
//│ res: (forall 'a. 'a -> 'b -> 'a) -> 'a0 -> 'b -> 'a0

fun (x: forall 'b 'a. 'a -> 'b -> 'a) -> x
//│ res: (forall 'a. 'a -> anything -> 'a) -> (forall 'a. 'a -> anything -> 'a)

// ============ Leijen 2007 ============

// ------------ Sec 2 ------------

// * FreezeML B1
// Not typeable in MLF
poly = fun f -> (f 1, f true)
//│ poly: (1 -> 'a & true -> 'b) -> ('a, 'b,)

// * FreezeML B2
polyL = fun xs -> poly (head xs)
//│ polyL: List[1 -> 'a & true -> 'b] -> ('a, 'b,)

let ids = single id in (polyL ids, append ids (single inc))
//│ res: ((1, true,), List[int -> int],)

// ------------ Sec 5 ------------

// * FreezeML C5
ids = single id
//│ ids: List[forall 'a. 'a -> 'a]

// * Let-bound version of FreezeML A3
let f = choose nil in f ids
//│ res: List[forall 'a. 'a -> 'a]

// * FreezeML A3
choose nil ids
//│ res: List[forall 'a. 'a -> 'a]

def g: (int -> (forall 'a. 'a -> 'a)) -> int
//│ g: (int -> (forall 'a. 'a -> 'a)) -> int

g (fun x -> id)
//│ res: int

let f = fun x -> id in g f
//│ res: int

// * FreezeML D4
app runST argST
//│ res: int


// ============ Variations ported from old Supertype prototype tests ============

killer_app = (fun x -> x id) auto
//│ killer_app: 'a -> 'a

I x = x
K x y = x
//│ I: 'a -> 'a
//│ K: 'a -> anything -> 'a

// """ In particular, we conjecture that the term ...
monster = (fun y -> (let tmp = y I in y K)) (fun x -> x x)
// """ ... that is typable in Fω but not in F [9] is not typable in MLF either.
//│ monster: anything -> 'a -> anything -> 'a

ignore x y = y
//│ ignore: anything -> 'a -> 'a

monsterThunk() = (fun y -> (ignore (y I) (y K))) (fun x -> x x)
//│ monsterThunk: () -> anything -> 'a -> anything -> 'a

monster2 = (fun y -> (y I, y K)) (fun x -> x x)
//│ monster2: (forall 'a. 'a -> 'a, forall 'b. anything -> 'b -> anything -> 'b,)

monster = (fun y -> (let tmp = y I in y K)) (fun x -> x x)
//│ monster: anything -> 'a -> anything -> 'a

monsterThunk() = (fun y -> ignore (y I) (y K)) (fun x -> x x)
//│ monsterThunk: () -> anything -> 'a -> anything -> 'a

monster2 = (fun y -> (y I, y K)) (fun x -> x x)
//│ monster2: (forall 'a. 'a -> 'a, forall 'b. anything -> 'b -> anything -> 'b,)

K K
//│ res: anything -> 'a -> anything -> 'a



// ============ A new look on MLF (2008 slides) ============


// def pack : forall α. forall β. (α, (α -> β)) -> (forall γ. (forall δ. (δ, (δ -> β)) -> γ) -> γ)

type Pair[A,B] = (A,B)
def pack : forall α. forall β. (α, (α -> β)) -> (forall γ. (forall δ. Pair[δ, (δ -> β)] -> γ) -> γ)
//│ Defined type alias Pair[+A, +B]
//│ pack: ('α, 'α -> 'β,) -> (forall 'δ. Pair['δ, 'δ -> 'β] -> 'γ) -> 'γ

def packed_int = pack (1, fun x -> x + 1)
//│ packed_int: (forall 'δ. Pair['δ, 'δ -> int] -> 'γ) -> 'γ

def packed_pair = pack (1, fun x -> (x, x ))
//│ packed_pair: (forall 'δ. Pair['δ, 'δ -> (1, 1,)] -> 'γ) -> 'γ

def fst((x,y)) = x
def snd((x,y)) = y
//│ fst: ('a, anything,) -> 'a
//│ snd: (anything, 'a,) -> 'a

v = packed_int (fun p -> (snd p) ( fst p ))
//│ v: int


def a: ' -> '
//│ a: ' -> '



