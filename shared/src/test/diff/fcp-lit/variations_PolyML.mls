// * Alternative: separate `Cons` and `Nil` types with definition of `mem` and `fold_left`
// * :RecursiveTypes is now needed due to the structural typing of Cons and Nil

// :NoRecursiveTypes
:NoJS


// ============ Sec 4.2 ============

// ------------ Dummy classes to represent the types in the examples ------------

class Nil: {}
class Cons[a]: { head: a; tail: List[a] }
type List[a] = Nil | Cons[a]
//│ Defined class Nil
//│ Defined class Cons[+a]
//│ Defined type alias List[+a]

def Nil = Nil {}
//│ Nil: Nil

def Cons head tail = Cons { head; tail }
//│ Cons: ('head & 'a) -> (List['a] & 'tail) -> (Cons['a] with {head: 'head, tail: 'tail})

// ------------ Type signatures for functions used in the examples ------------

rec def mem x l = case l of
  { Nil -> false
  | Cons -> if eq l.head x then true else mem x l.tail
  }
//│ mem: anything -> 'a -> Bool
//│   where
//│     'a <: (Cons[?] with {tail: 'a}) | Nil

rec def fold_left f x l = case l of
  { Nil -> x
  | Cons -> fold_left f (f x l.head) l.tail
  }
//│ fold_left: ('a -> 'head -> 'a) -> 'a -> 'b -> 'a
//│   where
//│     'b <: (Cons[?] with {head: 'head, tail: 'b}) | Nil

// ------------ polymorphic methods ------------

class Collection[a]: { l: List[a] }
  method Mem x = mem x this.l
  method Fold f x = fold_left f x this.l
//│ Defined class Collection[+a]
//│ Defined Collection.Mem: Collection[?] -> anything -> Bool
//│ Defined Collection.Fold: Collection['a] -> ('b -> 'a -> 'b) -> 'b -> 'b

def coll_mem c x = c.Mem x
//│ coll_mem: Collection[?] -> anything -> Bool

// * Typo in the paper? it was `fun x -> fun y -> ...`
def simple_and_double c =
  let l1 = c.Fold (fun y -> fun x -> Cons x y) Nil in
  let l2 = c.Fold (fun y -> fun x -> Cons ((x, x),) y) Nil in
  (l1, l2)
//│ simple_and_double: Collection['a] -> (forall 'b. Nil | 'b, forall 'c. Nil | 'c,)
//│   where
//│     'c :> Cons[('a, 'a,)] with {tail: forall 'c. Nil | 'c}
//│     'b :> Cons['a] with {tail: forall 'b. Nil | 'b}



// * Note: the kind of errors we get witout recursive types:

:NoRecursiveTypes

:e
rec def mem x l = case l of
  { Nil -> false
  | Cons -> if eq l.head x then true else mem x l.tail
  }
//│ ╔══[ERROR] Inferred recursive type: 'a
//│   where
//│     'a <: {head: ?, tail: Cons[?] & 'a}
//│ ║  l.72: 	rec def mem x l = case l of
//│ ╙──      	                       ^
//│ mem: anything -> 'a -> Bool
//│   where
//│     'a <: (Cons[?] with {tail: 'a}) | Nil


