:NewDefs


mut let i = 0
//│ mut let i: 0
//│ i
//│   = 0

set i = 1
//│ ()
//│ res
//│     = undefined

i
//│ 0 | 1
//│ res
//│     = 1


mut let f(x) =
  set f = _ => x
  x
//│ mut let f: 'a -> 'a
//│ f
//│   = [Function: f]

f(1)
//│ 1
//│ res
//│     = 1

f(true)
//│ 1 | true
//│ res
//│     = true


class Some[A](value: A)
module None
//│ class Some[A](value: A)
//│ module None

mut let oops = None
//│ mut let oops: None
//│ oops
//│      = None { class: [class None] }

fun funny(x) =
  set oops = Some(x)
  x
//│ fun funny: forall 'a. 'a -> 'a

funny(1)
//│ 1
//│ res
//│     = 1

if oops is
  None then 0
  Some(v) then v
//│ 0 | 1 & ??A
//│ res
//│     = 1

set oops = Some(123)
//│ ()
//│ res
//│     = undefined

oops
//│ None | Some['A & (1 | 'A0)]
//│   where
//│     'A :> 123
//│ res
//│     = Some {}



mut let oops = None
//│ mut let oops: None
//│ oops
//│      = None { class: [class None] }

fun funny(x) =
  let tmp = oops
  set oops = Some(x)
  tmp
//│ fun funny: 'A -> (None | Some['A])

funny(123)
//│ None | Some[123 | 'A]
//│ res
//│     = None { class: [class None] }

if funny("hi") is
  Some(v) then v
  else 0
//│ 0 | ??A & ("hi" | 123)
//│ res
//│     = 123


module None
//│ module None

mut let m = None
//│ mut let m: None
//│ m
//│   = None { class: [class None] }

mut val m = None
fun oops(x) = set m = x
//│ mut val m: None | 'm
//│ fun oops: 'm -> ()
//│ m
//│   = None { class: [class None] }


:e
let x = 1
set x = 2
x
//│ ╔══[ERROR] definition `x` is not mutable and cannot be reassigned
//│ ║  l.121: 	set x = 2
//│ ╙──       	^^^^^^^^^
//│ ╔══[ERROR] Type mismatch in assignment:
//│ ║  l.121: 	set x = 2
//│ ║         	^^^^^^^^^
//│ ╟── integer literal of type `2` does not match type `1`
//│ ║  l.121: 	set x = 2
//│ ║         	        ^
//│ ╟── Note: constraint arises from integer literal:
//│ ║  l.120: 	let x = 1
//│ ║         	        ^
//│ ╟── from definition of let binding x:
//│ ║  l.120: 	let x = 1
//│ ╙──       	    ^^^^^
//│ let x: 1
//│ 1
//│ x
//│   = 1
//│ res
//│     = undefined
//│ res
//│     = 2


mut let min = 123
//│ mut let min: 123
//│ min
//│     = 123

set min = 42
//│ ()
//│ res
//│     = undefined

min
//│ 123 | 42
//│ res
//│     = 42


fun repmin(t) =
  mut let min = 123
  let go(t) = set min = t.value
  go(t)
  min
//│ fun repmin: forall 'min. {value: 'min} -> (123 | 'min)


class Leaf[out A](val value: A)
//│ class Leaf[A](value: A)

fun repmin(t) =
  mut let min = 123
  let go(t) = if t is
    Leaf(n) then set min = n
  go(t)
  min
//│ fun repmin: forall 'min. Leaf['min | ~??A] -> (123 | 'min)

fun repmin =
  mut let min = 123
  let go(t) = if t is
    Leaf(n) then set min = n
  [min, go]
//│ fun repmin: forall 'min. [123 | 'min, Leaf['min | ~??A] -> ()]


:e
mut val lol[A] = (x: A) => x
//│ ╔══[ERROR] Type parameters are not yet supported in this position
//│ ║  l.192: 	mut val lol[A] = (x: A) => x
//│ ╙──       	            ^
//│ mut val lol: (x: 'A) -> 'A
//│ lol
//│     = [Function: lol]



:NoJS


mut let foo_ty = error
//│ mut let foo_ty: nothing

:ns
let rec body(x) =
  log of x.m + 1
  set foo_ty = body
  x
//│ let rec body: forall 'a 'm. 'a -> 'a
//│   where
//│     'a :> 'b
//│        <: 'c & {m: 'm}
//│     'm :> 'm0
//│        <: Int
//│     'c :> 'b
//│     'b <: {m: 'm0}
//│     'm0 <: Int

foo_ty of {m: 1}
//│ {m: 1}

foo_ty of {m: 2}
//│ {m: 1 | 2}

body({m: 3})
//│ {m: 1 | 2 | 3}

body({m: 4})
//│ {m: 1 | 2 | 4}

body
//│ forall 'a. ({m: Int} & 'a) -> ({m: 1 | 2} | 'a)

foo_ty
//│ ({m: Int} & 'a) -> ({m: 1 | 2 | 3 | 4} | 'a)



