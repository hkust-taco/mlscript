:NewDefs


class C<A>
//│ class C[A] {
//│   constructor()
//│ }

fun f(x) = if x is C then x
//│ fun f: forall 'A. C['A] -> C['A]
//│ f

// * TODO parse class tags?
// f(C : #C)


class C<A>(val a: A)
//│ class C[A](a: A)

let c = C(1)
//│ let c: C[1]
//│ c
//│   = C {}

c.a
//│ 1
//│ res
//│     = 1

fun f(x) = if x is C(a) then a
//│ fun f: forall 'a. C['a] -> 'a
//│ f

f(c)
//│ 1
//│ res
//│     = 1


class C[A](val n: A) {
  fun f = this.n
  fun g = C(12).n
}
//│ class C[A](n: A) {
//│   fun f: A
//│   fun g: 12
//│ }


class Some<A>(val value: A) {
  fun get = value
  fun toArray = [value]
  fun map(f) = Some(f(value))
  fun map_A(f : A => 'b) = Some(f(value))
}
//│ class Some[A](value: A) {
//│   fun get: A
//│   fun map: forall 'A. (A -> 'A) -> Some['A]
//│   fun map_A: forall 'A0. (A -> 'A0) -> Some['A0]
//│   fun toArray: [A]
//│ }


let s = Some(1)
//│ let s: Some[1]
//│ s
//│   = Some {}


s.value
//│ 1
//│ res
//│     = 1

s.get
//│ 1
//│ res
//│     = 1

s.toArray
//│ [1]
//│ res
//│     = [ 1 ]


s.map
//│ forall 'A. (1 -> 'A) -> Some['A]
//│ res
//│     = [Function: map]

s.map(succ)
//│ Some[Int]
//│ res
//│     = Some {}


s.map_A
//│ forall 'A. (1 -> 'A) -> Some['A]
//│ res
//│     = [Function: map_A]

s.map_A(succ)
//│ Some[Int]
//│ res
//│     = Some {}



module None {
  fun get = error
  fun toArray = []
  fun map(f) = None
  fun map_A(f: nothing -> anything) = None
}
//│ module None {
//│   fun get: nothing
//│   fun map: anything -> None
//│   fun map_A: (f: nothing -> anything) -> None
//│   fun toArray: []
//│ }


None.toArray
//│ []
//│ res
//│     = []


type Option<A> = Some<A> | None
//│ type Option[A] = None | Some[A]



let opt = if true then Some(123) else None
//│ let opt: None | Some[123]
//│ opt
//│     = Some {}

opt.toArray
//│ Array[123]
//│ res
//│     = [ 123 ]


opt.map(succ)
//│ None | Some[Int]
//│ res
//│     = Some {}

opt.map_A(succ)
//│ None | Some[Int]
//│ res
//│     = Some {}

opt.map(x => x > 0)
//│ None | Some[Bool]
//│ res
//│     = Some {}



if opt is Some then opt.value else 0
//│ 0 | 123
//│ res
//│     = 123

if opt is Some(v) then v else 0
//│ 0 | 123
//│ res
//│     = 123


fun map(x, f) = if x is
  None then None
  Some(v) then Some(f(v))
//│ fun map: forall 'a 'A. (None | Some['a], 'a -> 'A) -> (None | Some['A])
//│ map

let mo = map(opt, succ)
//│ let mo: None | Some[Int]
//│ mo
//│    = Some {}

mo.toArray
//│ Array[Int]
//│ res
//│     = [ 124 ]



// TODO
class Test(n) {
  fun foo = n + 1
  fun bar = n
}
//│ ╔══[ERROR] Class parameters currently need type annotations
//│ ║  l.192: 	class Test(n) {
//│ ╙──       	           ^
//│ class Test(n: error) {
//│   fun bar: error
//│   fun foo: Int
//│ }

Test(1)
//│ Test
//│ res
//│     = Test {}

// :e
Test(true)
//│ Test
//│ res
//│     = Test {}


:e
class Test<A>(n: A) {
  fun foo = n + 1
}
//│ ╔══[ERROR] Type mismatch in operator application:
//│ ║  l.218: 	  fun foo = n + 1
//│ ║         	            ^^^^^
//│ ╟── reference of type `A` is not an instance of type `Int`
//│ ║  l.218: 	  fun foo = n + 1
//│ ║         	            ^
//│ ╟── Note: type parameter A is defined at:
//│ ║  l.217: 	class Test<A>(n: A) {
//│ ╙──       	           ^
//│ class Test[A](n: A) {
//│   fun foo: Int | error
//│ }

Test(1)
//│ Test[1]
//│ res
//│     = Test {}

Test(true)
//│ Test[true]
//│ res
//│     = Test {}


class Test<A>(n: A) {
  fun foo: A = n
  fun foo1(x: A) = x
  fun id(x) = x
}
//│ class Test[A](n: A) {
//│   fun foo: A
//│   fun foo1: (x: A) -> A
//│   fun id: forall 'a. 'a -> 'a
//│ }

Test(1)
//│ Test[1]
//│ res
//│     = Test {}

Test(1).foo
//│ 1
//│ res
//│     = 1

Test("ok").foo
//│ "ok"
//│ res
//│     = 'ok'

let t = Test(1)
//│ let t: Test[1]
//│ t
//│   = Test {}

t.foo1(true)
//│ 1 | true
//│ res
//│     = true

t : Test<'a>
//│ Test['a]
//│   where
//│     'a :> 1 | true
//│ res
//│     = Test {}

t.id
//│ forall 'a. 'a -> 'a
//│ res
//│     = [Function: id]

[t.id(1), t.id(true)]
//│ [1, true]
//│ res
//│     = [ 1, true ]


:e
class TestBad<A>() {
  fun foo1(x: A) = x
  fun foo2(x: A) = x + 1
}
//│ ╔══[ERROR] Type mismatch in operator application:
//│ ║  l.301: 	  fun foo2(x: A) = x + 1
//│ ║         	                   ^^^^^
//│ ╟── reference of type `A` is not an instance of type `Int`
//│ ║  l.301: 	  fun foo2(x: A) = x + 1
//│ ║         	                   ^
//│ ╟── Note: type parameter A is defined at:
//│ ║  l.299: 	class TestBad<A>() {
//│ ╙──       	              ^
//│ class TestBad[A]() {
//│   fun foo1: (x: A) -> A
//│   fun foo2: (x: A) -> (Int | error)
//│ }

TestBad().foo1
//│ (x: 'A) -> 'A
//│ res
//│     = [Function: foo1]

TestBad().foo1(1)
//│ 1
//│ res
//│     = 1

x => TestBad().foo1(x)
//│ 'a -> 'a
//│ res
//│     = [Function: res]

// :d
let t = TestBad()
//│ let t: forall 'A. TestBad['A]
//│ t
//│   = TestBad {}

t.foo1
//│ (x: 'A) -> 'A
//│ res
//│     = [Function: foo1]

[t.foo1(0), t.foo1(true)]
//│ [0, true]
//│ res
//│     = [ 0, true ]

t.foo1(0)
//│ 0
//│ res
//│     = 0

t
//│ forall 'A. TestBad['A]
//│ res
//│     = TestBad {}


fun foo(x: TestBad<Int>) = x.foo1
//│ fun foo: (x: TestBad[Int]) -> (x: Int) -> Int
//│ foo

foo(t)
//│ (x: Int) -> Int
//│ res
//│     = [Function: foo1]

foo(t)(1)
//│ Int
//│ res
//│     = 1


TestBad().foo2
//│ (x: anything) -> (Int | error)
//│ res
//│     = [Function: foo2]



class Weird(val x: C<'a>)
//│ class Weird(x: C['a])

let w = Weird(c)
//│ let w: Weird
//│ w
//│   = Weird {}

w.x
//│ C['a]
//│ res
//│     = C {}

not(w.x.n)
//│ Bool
//│ res
//│     = true

:e
not(w.x.a)
//│ ╔══[ERROR] Type `C['a]` does not contain member `a`
//│ ║  l.400: 	not(w.x.a)
//│ ╙──       	       ^^
//│ Bool
//│ res
//│     = false



