:NewDefs
:NoJS


class Add0<out E>(lhs: E)
//│ class Add0[E](lhs: E)

fun eval(e) = if e is Add0(l) then eval(l)
//│ fun eval: forall 'a. 'a -> nothing
//│   where
//│     'a <: Add0['a]


class Add<out E>(lhs: E, rhs: E)
class Lit(val value: Int)
//│ class Add[E](lhs: E, rhs: E)
//│ class Lit(value: Int)

let add11 = Add(Lit(1), Lit(2))
//│ let add11: Add[Lit]

fun eval(e) =
  if e is
    Lit(n) then n: Int
    Add(l, r) then eval(l) + eval(r)
//│ fun eval: forall 'a. 'a -> Int
//│   where
//│     'a <: Lit | Add['a]


mixin EvalLit {
  fun eval(e) =
    if e is
      Lit(n) then n
}
//│ mixin EvalLit() {
//│   fun eval: Lit -> Int
//│ }

mixin EvalLit {
  fun eval(e: Lit) = e.value
}
//│ mixin EvalLit() {
//│   fun eval: (e: Lit) -> Int
//│ }


mixin EvalAdd {
  fun eval(e) =
    if e is
      Add(l, r) then this.eval(l)
}
//│ mixin EvalAdd() {
//│   this: {eval: 'a -> 'b}
//│   fun eval: Add['a] -> 'b
//│ }

module TestLang extends EvalAdd
//│ module TestLang {
//│   fun eval: 'a -> nothing
//│ }
//│ where
//│   'a <: Add['a]

TestLang.eval
//│ 'a -> nothing
//│   where
//│     'a <: Add['a]



mixin EvalBase {
  fun eval(e) =
    if e is
      Lit(n) then n: Int
      Add(l, r) then this.eval(l) + this.eval(r)
}
//│ mixin EvalBase() {
//│   this: {eval: 'a -> Int}
//│   fun eval: (Lit | Add['a]) -> Int
//│ }


module TestLang extends EvalBase
//│ module TestLang {
//│   fun eval: 'a -> Int
//│ }
//│ where
//│   'a <: Lit | Add['a]

TestLang.eval
//│ 'a -> Int
//│   where
//│     'a <: Lit | Add['a]



add11
//│ Add[Lit]

TestLang.eval(add11)
//│ Int

add11
//│ Add[Lit]

TestLang.eval(add11)
//│ Int

add11
//│ Add[Lit]



class Neg<out A>(expr: A)
//│ class Neg[A](expr: A)

let add2negadd11 = Add(Lit(2), Neg(add11))
//│ let add2negadd11: Add[Lit | Neg[Add[Lit]]]


mixin EvalNeg {
  fun eval(e) =
    if e is Neg(d) then 0 - this.eval(d)
    else super.eval(e)
}
//│ mixin EvalNeg() {
//│   super: {eval: 'a -> 'b}
//│   this: {eval: 'c -> Int}
//│   fun eval: (Neg['c] | Object & 'a & ~#Neg) -> (Int | 'b)
//│ }


module TestLang extends EvalBase, EvalNeg
//│ module TestLang {
//│   fun eval: 'a -> Int
//│ }
//│ where
//│   'a <: Neg['a] | Object & (Lit | Add['a]) & ~#Neg

TestLang.eval
//│ 'a -> Int
//│   where
//│     'a <: Neg['a] | Object & (Lit | Add['a]) & ~#Neg


TestLang.eval(add11)
//│ Int

TestLang.eval(Neg(add11))
//│ Int

TestLang.eval(Add(Lit(2), Neg(Lit(1))))
//│ Int

TestLang.eval(Neg(Neg(add11)))
//│ Int


TestLang.eval(add2negadd11)
//│ Int

TestLang.eval(Add(Lit(2), Neg(add11)))
//│ Int


