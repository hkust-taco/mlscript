:NewDefs
:NoJS


class Add0<out E>(lhs: E)
//│ class Add0[E](lhs: E)

fun eval(e) = if e is Add0(l) then eval(l)
//│ fun eval: forall 'E. Add0['E] -> nothing
//│   where
//│     'E <: Add0['E] | ~??E


class Add<out E>(lhs: E, rhs: E)
class Lit(val value: Int)
//│ class Add[E](lhs: E, rhs: E)
//│ class Lit(value: Int)

let add11 = Add(Lit(1), Lit(2))
//│ let add11: Add[Lit]

fun eval(e) =
  if e is
    Lit(n) then n: Int
    Add(l, r) then eval(l) + eval(r)
//│ fun eval: forall 'E. (Add['E] | Lit) -> Int
//│   where
//│     'E <: Add['E] | Lit | ~??E


mixin EvalLit {
  fun eval(e) =
    if e is
      Lit(n) then n
}
//│ mixin EvalLit() {
//│   fun eval: Lit -> Int
//│ }

mixin EvalLit {
  fun eval(e: Lit) = e.value
}
//│ mixin EvalLit() {
//│   fun eval: (e: Lit) -> Int
//│ }


mixin EvalAdd {
  fun eval(e) =
    if e is
      Add(l, r) then this.eval(l)
}
//│ mixin EvalAdd() {
//│   this: {eval: (??E & 'E) -> 'a}
//│   fun eval: Add['E] -> 'a
//│ }

module TestLang extends EvalAdd
//│ module TestLang {
//│   fun eval: Add['E] -> nothing
//│ }
//│ where
//│   'E <: Add['E] | ~??E

TestLang.eval
//│ Add['E] -> nothing
//│   where
//│     'E <: Add['E] | ~??E



mixin EvalBase {
  fun eval(e) =
    if e is
      Lit(n) then n: Int
      Add(l, r) then this.eval(l) + this.eval(r)
}
//│ mixin EvalBase() {
//│   this: {eval: (??E & 'E) -> Int}
//│   fun eval: (Add['E] | Lit) -> Int
//│ }


module TestLang extends EvalBase
//│ module TestLang {
//│   fun eval: (Add['E] | Lit) -> Int
//│ }
//│ where
//│   'E <: Add['E] | Lit | ~??E

TestLang.eval
//│ (Add['E] | Lit) -> Int
//│   where
//│     'E <: Add['E] | Lit | ~??E



add11
//│ Add[Lit]

TestLang.eval(add11)
//│ Int

add11
//│ Add[Lit]

TestLang.eval(add11)
//│ Int

add11
//│ Add[Lit]



class Neg<out A>(expr: A)
//│ class Neg[A](expr: A)

let add2negadd11 = Add(Lit(2), Neg(add11))
//│ let add2negadd11: Add[Lit | Neg[Add[Lit]]]


mixin EvalNeg {
  fun eval(e) =
    if e is Neg(d) then 0 - this.eval(d)
    else super.eval(e)
}
//│ mixin EvalNeg() {
//│   super: {eval: 'a -> 'b}
//│   this: {eval: (??A & 'A) -> Int}
//│   fun eval: (Neg['A] | Object & 'a & ~#Neg) -> (Int | 'b)
//│ }


module TestLang extends EvalBase, EvalNeg
//│ module TestLang {
//│   fun eval: (Neg['A] | Object & 'a & ~#Neg) -> Int
//│ }
//│ where
//│   'A <: Neg['A] | Object & 'a & ~#Neg | ~??A
//│   'a <: Add[Neg['A] | Object & 'a & ~#Neg | ~??E] | Lit

TestLang.eval
//│ (Neg['A] | Object & 'a & ~#Neg) -> Int
//│   where
//│     'A <: Neg['A] | Object & 'a & ~#Neg | ~??A
//│     'a <: Add[Neg['A] | Object & 'a & ~#Neg | ~??E] | Lit


TestLang.eval(add11)
//│ Int

TestLang.eval(Neg(add11))
//│ Int

TestLang.eval(Add(Lit(2), Neg(Lit(1))))
//│ Int

TestLang.eval(Neg(Neg(add11)))
//│ Int


TestLang.eval(add2negadd11)
//│ Int

TestLang.eval(Add(Lit(2), Neg(add11)))
//│ Int


