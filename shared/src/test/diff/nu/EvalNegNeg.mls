:NewDefs


class Add<out E>(lhs: E, rhs: E)
class Lit(n: Int)
//│ class Add[E](lhs: E, rhs: E)
//│ class Lit(n: Int)


mixin EvalBase {
  fun eval(e) =
    if e is
      Lit(n) then n: Int
      Add(l, r) then this.eval(l) + this.eval(r)
}
//│ mixin EvalBase() {
//│   this: {eval: 'a -> Int}
//│   fun eval: (Add['a] | Lit) -> Int
//│ }


class Neg<out A>(expr: A)
//│ class Neg[A](expr: A)


mixin EvalNeg {
  fun eval(e) =
    if e is Neg(d) then 0 - this.eval(d)
    else super.eval(e)
}
//│ mixin EvalNeg() {
//│   super: {eval: 'a -> 'b}
//│   this: {eval: 'c -> Int}
//│   fun eval: (Neg['c] | Object & 'a & ~#Neg) -> (Int | 'b)
//│ }


mixin EvalNegNeg {
  fun eval(e) =
    if e is Neg(Neg(d)) then this.eval(d)
    else super.eval(e)
}
//│ mixin EvalNegNeg() {
//│   super: {eval: (Neg[nothing] | 'a) -> 'b}
//│   this: {eval: 'c -> 'b}
//│   fun eval: (Neg[Neg['c] | Object & ~#Neg] | Object & 'a & ~#Neg) -> 'b
//│ }


module TestLang extends EvalBase, EvalNeg, EvalNegNeg
//│ module TestLang {
//│   fun eval: (Neg['A] | Object & 'a & ~#Neg) -> Int
//│ }
//│ where
//│   'A <: 'b & (Neg['b] | Object & ~#Neg)
//│   'b <: Neg['A] | Object & 'a & ~#Neg
//│   'a <: Add['b] | Lit | Neg['b]


fun mk(n) = if n is
  0 then Lit(3)
  1 then Neg(mk(n - 1))
  _ then Add(mk(n - 1), mk(n - 1))
//│ fun mk: forall 'a. (0 | 1 | Int & ~0 & ~1) -> (Lit | 'a)
//│   where
//│     'a :> Add[Lit | 'a] | Neg[Lit | 'a]

TestLang.eval(mk(0))
//│ Int
//│ res
//│     = 3

TestLang.eval(mk(11))
//│ Int
//│ res
//│     = -3072

