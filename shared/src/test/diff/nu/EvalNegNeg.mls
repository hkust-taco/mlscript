:NewDefs


class Add<E>(lhs: E, rhs: E)
class Lit(n: Int)
//│ class Add[E](lhs: E, rhs: E)
//│ class Lit(n: Int)


mixin EvalBase {
  fun eval(e) =
    if e is
      Lit(n) then n: Int
      Add(l, r) then this.eval(l) + this.eval(r)
}
//│ mixin EvalBase() {
//│   this: {eval: 'a ->{'b} Int}
//│   fun eval: (Add['a] | Lit) ->{'b} Int
//│ }


class Neg<A>(expr: A)
//│ class Neg[A](expr: A)


mixin EvalNeg {
  fun eval(e) =
    if e is Neg(d) then 0 - this.eval(d)
    else super.eval(e)
}
//│ mixin EvalNeg() {
//│   super: {eval: 'a ->{'c} 'b}
//│   this: {eval: 'd ->{'c} Int}
//│   fun eval: (Neg['d] | Object & 'a & ~#Neg) ->{'c} (Int | 'b)
//│ }


mixin EvalNegNeg {
  fun eval(e) =
    if e is Neg(Neg(d)) then this.eval(d)
    else super.eval(e)
}
//│ mixin EvalNegNeg() {
//│   super: {eval: (Neg[nothing] | 'a) ->{'c} 'b}
//│   this: {eval: 'd ->{'c} 'b}
//│   fun eval: (Neg[Neg['d] | Object & ~#Neg] | Object & 'a & ~#Neg) ->{'c} 'b
//│ }


module TestLang extends EvalBase, EvalNeg, EvalNegNeg
//│ module TestLang {
//│   fun eval: (Neg['A] | Object & 'a & ~#Neg) -> Int
//│ }
//│ where
//│   'A <: Neg['A & 'b] | Neg['A] & ~#Neg | Object & 'a & ~#Neg
//│   'b <: Neg['A] | Object & 'a & ~#Neg
//│   'a <: Add['b] | Lit | Neg['b]


fun mk(n) = if n is
  0 then Lit(3)
  1 then Neg(mk(n - 1))
  _ then Add(mk(n - 1), mk(n - 1))
//│ fun mk: forall 'a. (0 | 1 | Int & ~0 & ~1) -> (Lit | 'a)
//│   where
//│     'a :> Neg[Lit | 'a] | Add[Lit | 'a]

TestLang.eval(mk(0))
//│ Int
//│ res
//│     = 3

TestLang.eval(mk(11))
//│ Int
//│ res
//│     = -3072

