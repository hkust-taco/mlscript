
class Parent: { name: string }
//│ Defined class Parent

class Child: Parent & { age: int }
//│ Defined class Child

def p = Parent{ name = "Bob" }
//│ p: Parent & {name: "Bob"}

p: Parent
//│ res: Parent

:e
def c = Child(Parent { name = "Bob" })
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.15: 	def c = Child(Parent { name = "Bob" })
//│ ║        	        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── expression of type `Parent & {name: ?name}` does not match type `{name: ?name0, age: ?age}`
//│ ║  l.15: 	def c = Child(Parent { name = "Bob" })
//│ ╙──      	              ^^^^^^^^^^^^^^^^^^^^^^^
//│ c: Child & {age: nothing, name: "Bob"} | error

def c = Child(Parent { name = "Bob" } with { age = 123 })
//│ c: Child & {age: 123, name: "Bob"}

c: Parent
//│ res: Parent

c.name
//│ res: "Bob"

c: { name: 'a }
//│ res: {name: "Bob"}

c: { name: string }
//│ res: {name: string}


def f x = case x of {
  | Parent -> x.name
  }
//│ f: (Parent with {name: 'a}) -> 'a

f c
//│ res: "Bob"

f (c: Parent)
//│ res: string


def f x = case x of {
  | Child -> x.name
  }
//│ f: (Child\age with {name: 'a}) -> 'a

f c
//│ res: "Bob"

:e
f (c: Parent)
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.61: 	f (c: Parent)
//│ ║        	^^^^
//│ ╟── expression of type `Parent` does not match type `Child`
//│ ║  l.61: 	f (c: Parent)
//│ ║        	      ^^^^^^
//│ ╟── but it flows into argument with expected type `(?a,)`
//│ ║  l.61: 	f (c: Parent)
//│ ║        	   ^
//│ ╟── Note: constraint arises from class pattern:
//│ ║  l.53: 	  | Child -> x.name
//│ ║        	    ^^^^^
//│ ╟── from reference:
//│ ║  l.52: 	def f x = case x of {
//│ ╙──      	               ^
//│ res: error | string


def f x = case x of {
  | Parent -> x.name
  | _ -> ""
  }
//│ f: ((Parent with {name: 'a}) | ~Parent) -> ("" | 'a)

f c
//│ res: "" | "Bob"

f (c: Parent)
//│ res: string


def f x = case x of {
  | Parent -> x.name
  | Child -> x.name
  }
//│ f: (Parent with {name: 'a}) -> 'a

f c
//│ res: "Bob"

f (c: Parent)
//│ res: string

