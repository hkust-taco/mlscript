
class Parent: { name: string }
//│ Defined class Parent

class Child: Parent & { age: int }
//│ Defined class Child

def p = Parent{ name = "Bob" }
//│ p: parent & {name: "Bob"}

p: Parent
//│ res: parent & {name: string}

:e
def c = Child(Parent { name = "Bob" })
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.15: 	def c = Child(Parent { name = "Bob" })
//│ ║        	        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── expression of type `parent & {name: ?a | "Bob"}` does not match type `{age: int & ?b, name: string & ?c}`
//│ ║  l.15: 	def c = Child(Parent { name = "Bob" })
//│ ╙──      	              ^^^^^^^^^^^^^^^^^^^^^^^
//│ c: child & {name: "Bob", age: nothing} | error

def c = Child(Parent { name = "Bob" } with { age = 123 })
//│ c: child & {name: "Bob", age: 123}

c: Parent
//│ res: parent & {name: string}

c.name
//│ res: "Bob"

c: { name: 'a }
//│ res: {name: "Bob"}

c: { name: string }
//│ res: {name: string}


def f x = case x of {
  | Parent -> x.name
  }
//│ f: parent & {name: 'a} -> 'a

f c
//│ res: "Bob"

f (c: Parent)
//│ res: string


def f x = case x of {
  | Child -> x.name
  }
//│ f: child & {name: 'a} -> 'a

f c
//│ res: "Bob"

:e
f (c: Parent)
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.61: 	f (c: Parent)
//│ ║        	^^^^
//│ ╟── expression of type `parent & {name: string}` does not match type `child`
//│ ║  l.61: 	f (c: Parent)
//│ ║        	      ^^^^^^
//│ ╟── but it flows into argument with expected type `child`
//│ ║  l.61: 	f (c: Parent)
//│ ║        	   ^
//│ ╟── Note: constraint arises from class pattern:
//│ ║  l.53: 	  | Child -> x.name
//│ ║        	    ^^^^^
//│ ╟── from reference:
//│ ║  l.52: 	def f x = case x of {
//│ ╙──      	               ^
//│ res: string | error


def f x = case x of {
  | Parent -> x.name
  | _ -> ""
  }
//│ f: parent & {name: 'a} | ~parent -> 'a | ""

f c
//│ res: "Bob" | ""

f (c: Parent)
//│ res: string


def f x = case x of {
  | Parent -> x.name
  | Child -> x.name
  }
//│ f: parent & {name: 'a} -> 'a

f c
//│ res: "Bob"

f (c: Parent)
//│ res: string

