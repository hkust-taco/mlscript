:NoJS
// :NoProvs


class C[A]
  method In: A -> A
  method In = id
//│ Defined class C[=A]
//│ Declared C.In: C['A] -> 'A -> 'A
//│ Defined C.In: C['A] -> 'a -> 'a

def c: C[C[int]]
//│ c: C[C[int]]

def c: 'a -> C[C['a]]
//│ c: 'a -> C[C['a]]

def c: C[C['a]] -> 'a
//│ c: C[C['a]] -> 'a

def c: C['a] as 'a
//│ c: 'a
//│   where
//│     'a := C['a]

// * [test:T3] What happens here is we use `factorize` to simplify the union,
// * which uses the fact that `C['a] <: 'a` to reduce `C['a] | 'a` to `'a`...
// * which is obviously NOT valid when that union we're processing is itself the definition of `'a`!
// * So we will need to be more careful about simplifying types, so as not to assume bounds
// * we are in the process of simplifying. This could be done by NOT simplifying things in place.
def c: C['a] | 'a as 'a
//│ c: 'a
//│   where
//│     'a := 'a

def c: C[C['a]] as 'a
//│ c: 'a
//│   where
//│     'a := C[C['a]]

def c: C[C['a] & 'a] as 'a
//│ c: 'a
//│   where
//│     'a := C[C['a] & 'a]

def c: C[C['a] & 'a | 'a] as 'a
//│ c: 'a
//│   where
//│     'a := C['a]

def c: C['a]
//│ c: C['a]


class C2[A]: { a: A }
  method In: A -> A
  method In = id
//│ Defined class C2[=A]
//│ Declared C2.In: C2['A] -> 'A -> 'A
//│ Defined C2.In: C2['A] -> 'a -> 'a


def mkC: 'a -> C2['a]
//│ mkC: 'a -> C2['a]

mkC' a = C2 { a }
//│ mkC': ('a & 'A) -> (C2['A] with {a: 'a})

mkC = mkC'
//│ ('a & 'A) -> (C2['A] with {a: 'a})
//│   <:  mkC:
//│ 'a -> C2['a]

rec def rc = mkC(rc)
//│ rc: C2['a]
//│   where
//│     'a :> C2['a]

rec def rc = mkC'(rc)
//│ rc: 'a
//│   where
//│     'a :> C2['A] & {a: forall 'a. 'a}
//│     'A :> forall 'a. 'a



class C3[A]: { a: C3[A] }
  method In: A -> A
  method In = id
//│ Defined class C3[=A]
//│ Declared C3.In: C3['A] -> 'A -> 'A
//│ Defined C3.In: C3['A] -> 'a -> 'a

def c: 'a -> C3['a]
//│ c: 'a -> C3['a]

rec def c a = C3 { a = c a }
//│ anything -> 'a
//│   where
//│     'a :> C3['A] with {a: 'a}
//│   <:  c:
//│ 'a -> C3['a]

rec def c (a: 'X) = C3 { a = c a: 'X }: C3['X]
//│ anything -> C3['X]
//│   <:  c:
//│ 'a -> C3['a]


class C4[A]: { a: C[C4[A]] }
  method In: A -> A
  method In = id
//│ Defined class C4[=A]
//│ Declared C4.In: C4['A] -> 'A -> 'A
//│ Defined C4.In: C4['A] -> 'a -> 'a

def c: 'a -> C4['a]
//│ c: 'a -> C4['a]

C{}
//│ res: C['A]

def c4 a = C4{ a = C{} }
//│ c4: anything -> (C4['A] with {a: forall 'A0. C['A0]})

def c = c4
//│ anything -> (C4['A] with {a: forall 'A0. C['A0]})
//│   <:  c:
//│ 'a -> C4['a]


class C5[A]: { a: C2[C5[A]] }
  method In: A -> A
  method In = id
//│ Defined class C5[=A]
//│ Declared C5.In: C5['A] -> 'A -> 'A
//│ Defined C5.In: C5['A] -> 'a -> 'a

def c: 'a -> C5['a]
//│ c: 'a -> C5['a]

rec def c5 a = C5{ a = C2 { a = c5 a } }
//│ c5: anything -> (C5['A] with {a: 'a})
//│   where
//│     'a :> C2['A0] with {a: C5['A] with {a: 'a}}
//│     'A0 :> (C5['A] with {a: 'a}) | C5['A]
//│         <: C5['A]

c = c5
//│ anything -> (C5['A] with {a: 'a})
//│   where
//│     'a :> C2['A0] with {a: C5['A] with {a: 'a}}
//│     'A0 :> (C5['A] with {a: 'a}) | C5['A]
//│         <: C5['A]
//│   <:  c:
//│ 'a -> C5['a]


class C6[A]: { a: C5[C6[A]] }
  method In: A -> A
  method In = id
//│ Defined class C6[=A]
//│ Declared C6.In: C6['A] -> 'A -> 'A
//│ Defined C6.In: C6['A] -> 'a -> 'a

def c: 'a -> C6['a]
//│ c: 'a -> C6['a]

rec def c6 a = C6{ a = c5 (c6 a) }
//│ c6: anything -> (C6['A] with {a: forall 'A0 'a 'A1. C5['A0] with {a: 'a}})
//│   where
//│     'a :> C2['A1] with {a: C5['A0] with {a: 'a}}
//│     'A1 :> (C5['A0] with {a: 'a}) | C5['A0]
//│         <: C5['A0]

:stats
c = c6
//│ anything -> (C6['A] with {a: forall 'A0 'a 'A1. C5['A0] with {a: 'a}})
//│   where
//│     'a :> C2['A1] with {a: C5['A0] with {a: 'a}}
//│     'A1 :> (C5['A0] with {a: 'a}) | C5['A0]
//│         <: C5['A0]
//│   <:  c:
//│ 'a -> C6['a]
//│ constrain calls  : 64
//│ annoying  calls  : 34
//│ subtyping calls  : 434


// Reproduction of an issue found while trying out TypeRef ctor typing:

def e: C5[C6['A]] | C5[C6['A]] & ~c5 | C5[C6['A]] | C5[C6['A]] & ~{a: C2[C5[C6['A]]]}
//│ e: C5[C6['A]]

def e: C5[C6['A]] & {a: C2[C5[C6['A]] | C5[C6['A]] & ~c5 | C5[C6['A]] | C5[C6['A]] & ~{a: C2[C5[C6['A]]]}] & {a: 'a}}
//│ e: C5[C6['A]] & {a: C2[C5[C6['A]]] & {a: nothing}}

type Inner = C5[C6['A]] & {a: C2[C5[C6['A]] | C5[C6['A]] & ~c5 | C5[C6['A]] | C5[C6['A]] & ~{a: C2[C5[C6['A]]]}] & {a: 'a}} as 'a
//│ Defined type alias Inner

def e: anything -> (C6['A] & {a: Inner})
//│ e: anything -> (C6['A] with {a: Inner})

// * The problem seems to come from Inner defining type variables, which are given level 0...
// *  This is actually the expected behavior of type vars in type aliases,
// *  but it's really weird and fishy so it should probably be rejected.
// :e // no longer an error...(?)
// :e // Fails with polymorphic RHS extrusion
// Used to fail with a depth excess... (before implicitly quantifying TVs in type defs)
c = e
//│ anything -> (C6['A] with {a: Inner})
//│   <:  c:
//│ 'a -> C6['a]

type Inner2[A] = C5[C6[A]] & {a: C2[C5[C6[A]] | C5[C6[A]] & ~c5 | C5[C6[A]] | C5[C6[A]] & ~{a: C2[C5[C6[A]]]}] & {a: Inner2[A]}}
//│ Defined type alias Inner2[=A]

def e: anything -> (C6['A] & {a: Inner2['A]})
//│ e: anything -> (C6['A] with {a: Inner2['A]})

c = e
//│ anything -> (C6['A] with {a: Inner2['A]})
//│   <:  c:
//│ 'a -> C6['a]



class N: {}
class S[T]: { v: T }
  method In: T -> ()
  method In _ = ()
type O[T] = S[T] | N
class L[T]: { h: T; t: O[L[T]] }
  method Append: T -> L[T]
  method Append elem = L { h = elem; t = S { v = this } }
//│ Defined class N
//│ Defined class S[=T]
//│ Declared S.In: S['T] -> 'T -> ()
//│ Defined S.In: S['T] -> anything -> ()
//│ Defined type alias O[=T]
//│ Defined class L[=T]
//│ Declared L.Append: L['T] -> 'T -> L['T]
//│ Defined L.Append: (L['T] & 'this) -> ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] & {v: L['T] & 'this}})

s1 = S{v=1}:O['_]
s2 = S{v=S{v=1}}:O[O['_]]
//│ s1: O['_]
//│   where
//│     '_ :> 1
//│ s2: O[O['_]]
//│   where
//│     '_ :> 1

:e
L{h=error;t=s1}
L{h=error;t=s2}
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.255: 	L{h=error;t=s1}
//│ ║         	^^^^^^^^^^^^^^^
//│ ╟── integer literal of type `1` is not an instance of type `L`
//│ ║  l.245: 	s1 = S{v=1}:O['_]
//│ ║         	         ^
//│ ╟── Note: constraint arises from applied type reference:
//│ ║  l.233: 	class L[T]: { h: T; t: O[L[T]] }
//│ ╙──       	                         ^^^^
//│ res: error
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.256: 	L{h=error;t=s2}
//│ ║         	^^^^^^^^^^^^^^^
//│ ╟── type `S['_]` is not an instance of type `L`
//│ ║  l.232: 	type O[T] = S[T] | N
//│ ║         	            ^^^^
//│ ╟── Note: constraint arises from applied type reference:
//│ ║  l.233: 	class L[T]: { h: T; t: O[L[T]] }
//│ ╙──       	                         ^^^^
//│ res: error

// :ds
// L.Append

// before:
//  Defined L.Append: L['T] -> ('T & 'a & 'b) -> (L['T & 'b .. 'T | 'b] with {h: 'a, t: S[L['T & 'b .. 'T | 'b] & 'c .. L['T & 'b .. 'T | 'b] | L['T] | 'c] with {v: L['T]}})

L.Append
//│ res: L['T] -> 'T -> L['T]


def append ls elem = L { h = elem; t = S { v = ls } }
//│ append: (L['T] & 'v) -> ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] with {v: 'v}})

:ns
append
//│ res: forall 'a 'b 'v 'T 'c 'h 'T0 't. 'a -> 'b -> 'c
//│   where
//│     'c :> #L & {h: 'h, t: 't, L#T = 'T0}
//│     't :> #S & {v: 'v, S#T = 'T}
//│        <: O[L['T0]]
//│     'b <: 'h
//│     'h <: 'T0
//│     'a <: 'v
//│     'v <: L['T0] & 'T
//│     'T := L['T0]

append error
//│ res: ('h & 'T) -> (L['T] with {h: 'h, t: S[L['T]] & {v: nothing}})


def append_ty: (L['T] & 'v) -> ('T & 'h) -> (L['T] & {h: 'h; t: S[L['T]] & {v: 'v}})
//│ append_ty: (L['T] & 'v) -> ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] with {v: 'v}})

append_ty error
//│ res: ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] & {v: nothing}})

append_ty = append
//│ (L['T] & 'v) -> ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] with {v: 'v}})
//│   <:  append_ty:
//│ (L['T] & 'v) -> ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] with {v: 'v}})


// * Note: An older bug (fixed in 2a562ddfc712ab44a55a12370380ef4f1c3383cb)
// *    was dropping the bounds on T0 and generating this signature, which is too general:
def append_ty_2: (L['T] & 'v & 'T0) -> ('T & 'h) -> (L['T] & {h: 'h; t: S['T0] & {v: 'v}})
//│ append_ty_2: (L['T] & 'v & 'T0) -> ('T & 'h) -> (L['T] with {h: 'h, t: S['T0] with {v: 'v}})

append_ty_2 error
//│ res: ('T & 'h) -> (L['T] with {h: 'h, t: S['T0] & {v: nothing}})

:e
append_ty_2 = append
//│ (L['T] & 'v) -> ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] with {v: 'v}})
//│   <:  append_ty_2:
//│ (L['T] & 'v & 'T0) -> ('T & 'h) -> (L['T] with {h: 'h, t: S['T0] with {v: 'v}})
//│ ╔══[ERROR] Type mismatch in def definition:
//│ ║  l.329: 	append_ty_2 = append
//│ ║         	^^^^^^^^^^^^^^^^^^^^
//│ ╟── type `'T0` is not an instance of type `L`
//│ ║  l.322: 	def append_ty_2: (L['T] & 'v & 'T0) -> ('T & 'h) -> (L['T] & {h: 'h; t: S['T0] & {v: 'v}})
//│ ║         	                                                                          ^^^
//│ ╟── Note: constraint arises from applied type reference:
//│ ║  l.233: 	class L[T]: { h: T; t: O[L[T]] }
//│ ║         	                         ^^^^
//│ ╟── Note: class type parameter T is defined at:
//│ ║  l.229: 	class S[T]: { v: T }
//│ ╙──       	        ^

append_ty = append_ty_2
//│ (L['T] & 'v & 'T0) -> ('T & 'h) -> (L['T] with {h: 'h, t: S['T0] with {v: 'v}})
//│   <:  append_ty:
//│ (L['T] & 'v) -> ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] with {v: 'v}})

:e
append_ty_2 = append_ty
//│ (L['T] & 'v) -> ('T & 'h) -> (L['T] with {h: 'h, t: S[L['T]] with {v: 'v}})
//│   <:  append_ty_2:
//│ (L['T] & 'v & 'T0) -> ('T & 'h) -> (L['T] with {h: 'h, t: S['T0] with {v: 'v}})
//│ ╔══[ERROR] Type mismatch in def definition:
//│ ║  l.352: 	append_ty_2 = append_ty
//│ ║         	^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── type `'T0` is not an instance of type `L`
//│ ║  l.322: 	def append_ty_2: (L['T] & 'v & 'T0) -> ('T & 'h) -> (L['T] & {h: 'h; t: S['T0] & {v: 'v}})
//│ ║         	                                                                          ^^^
//│ ╟── Note: constraint arises from applied type reference:
//│ ║  l.308: 	def append_ty: (L['T] & 'v) -> ('T & 'h) -> (L['T] & {h: 'h; t: S[L['T]] & {v: 'v}})
//│ ╙──       	                                                                  ^^^^^


def append0 ls = L { h = 0; t = S { v = ls } }
//│ append0: (L['T] & 'v) -> (L['T] with {h: 0, t: S[L['T]] with {v: 'v}})
//│   where
//│     'T :> 0

def appendNil elem = L { h = elem; t = N{} }
//│ appendNil: ('h & 'T) -> (L['T] with {h: 'h, t: N})


S{v=1}
//│ res: S['T] & {v: 1}
//│   where
//│     'T :> 1

