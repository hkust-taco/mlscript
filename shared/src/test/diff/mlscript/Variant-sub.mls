class None: {}
class Some[A]: { v: A }
//│ Defined class None
//│ Defined class Some

def Some v = Some{v}
def None = None{}
//│ Some: ('v & 'A) -> (Some['A] with {v: 'v})
//│     = [Function: Some1]
//│ None: None
//│     = None {}

def flatMap = fun f -> fun opt ->
  case opt of { Some -> f opt.v | None -> opt }
//│ flatMap: ('a -> 'b) -> (None & 'b | (Some[?] with {v: 'a})) -> 'b
//│        = [Function: flatMap]

f x = Some x
flatMap f (Some 1)
flatMap f None
//│ f: ('v & 'A) -> (Some['A] with {v: 'v})
//│  = [Function: f]
//│ res: Some['A] & {v: 1}
//│   where
//│     'A :> 1
//│    = Some { v: 1 }
//│ res: None | (Some['A] with {v: nothing})
//│    = None {}

:e
flatMap f "oops"
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.31: 	flatMap f "oops"
//│ ║        	^^^^^^^^^^^^^^^^
//│ ╟── string literal of type `"oops"` does not match type `None & ?a | Some[?] & ?b`
//│ ║  l.31: 	flatMap f "oops"
//│ ║        	          ^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.14: 	  case opt of { Some -> f opt.v | None -> opt }
//│ ╙──      	       ^^^
//│ res: error | (Some['A] with {v: nothing})
//│ Runtime error:
//│   Error: non-exhaustive case expression

class NoneBecause: None & { reason: string }
flatMap f (NoneBecause { reason = "uh uh" })
//│ Defined class NoneBecause
//│ res: NoneBecause & {reason: "uh uh"} | (Some['A] with {v: nothing})
//│    = NoneBecause { reason: 'uh uh' }

type Expr = Lit | Neg | Var | Plus
class Lit: {v: int}
class Neg: {sub: Expr}
class Var: {nme: string}
class Plus: {lhs: Expr; rhs: Expr}
//│ Defined type alias Expr
//│ Defined class Lit
//│ Defined class Neg
//│ Defined class Var
//│ Defined class Plus

rec def evalOpt x = case x of {
  | Lit ->
      Some x.v
  | Neg ->
      // flatMap (fun s -> Some (neg x.sub)) (evalOpt x.sub)
      flatMap (fun s -> Some (negate s)) (evalOpt x.sub)
  | Var ->
      // None with {err = concat "free var: " x.nme}
      NoneBecause {reason = concat "free var: " x.nme}
      // None
  | Plus ->
      flatMap (fun l -> flatMap (fun r ->
        // Some (add x.lhs x.rhs)
        Some (l + r)
      ) (evalOpt x.rhs)) (evalOpt x.lhs)
  }
//│ evalOpt: 'a -> (NoneBecause | (Some[in 'A out int | 'A] with {v: int}))
//│   where
//│     'a <: Lit & {v: int & 'A} | (Neg with {sub: 'a}) | (Plus with {lhs: 'a, rhs: 'a}) | Var
//│        = [Function: evalOpt]

evalOpt (Plus{lhs = Lit{v=2}; rhs = Lit{v=3}})
//│ res: NoneBecause | (Some[in 'A out int | 2 | 3 | 'A] with {v: int})
//│    = Some { v: 5 }
