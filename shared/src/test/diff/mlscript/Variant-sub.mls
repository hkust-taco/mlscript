class None: {}
class Some[A]: { v: A }
//│ Defined class None
//│ Defined class Some

def Some v = Some{v}
def None = None{}
//│ Some: 'a -> (Some['A .. 'A | 'a] with {v: 'a})
//│ None: None

def flatMap = fun f -> fun opt ->
  case opt of { Some -> f opt.v | None -> opt }
//│ flatMap: ('a -> 'b) -> (None & 'b | (Some[?] with {v: 'a})) -> 'b

f x = Some x
flatMap f (Some 1)
flatMap f None
//│ f: 'a -> (Some['A .. 'A | 'a] with {v: 'a})
//│ res: Some['A .. 1 | 'A] with {v: 1}
//│ res: None | (Some['A] with {v: nothing})

:e
flatMap f "oops"
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.23: 	flatMap f "oops"
//│ ║        	^^^^^^^^^^^^^^^^
//│ ╟── expression of type `"oops"` does not match type `Some[?] & ?a | None & ?b`
//│ ║  l.23: 	flatMap f "oops"
//│ ║        	          ^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.12: 	  case opt of { Some -> f opt.v | None -> opt }
//│ ╙──      	       ^^^
//│ res: "oops" | error | (Some['A] with {v: nothing})

class NoneBecause: None & { reason: string }
flatMap f (NoneBecause { reason = "uh uh" })
//│ Defined class NoneBecause
//│ res: NoneBecause & {reason: "uh uh"} | (Some['A] with {v: nothing})

type Expr = Lit | Neg | Var | Plus
class Lit: {v: int}
class Neg: {sub: Expr}
class Var: {nme: string}
class Plus: {lhs: Expr; rhs: Expr}
//│ Defined type alias Expr
//│ Defined class Lit
//│ Defined class Neg
//│ Defined class Var
//│ Defined class Plus

rec def evalOpt x = case x of {
  | Lit ->
      Some x.v
  | Neg ->
      // flatMap (fun s -> Some (neg x.sub)) (evalOpt x.sub)
      flatMap (fun s -> Some (0 - s)) (evalOpt x.sub)
  | Var ->
      // None with {err = concat "free var: " x.nme}
      NoneBecause {reason = concat "free var: " x.nme}
      // None
  | Plus ->
      flatMap (fun l -> flatMap (fun r ->
        // Some (add x.lhs x.rhs)
        Some (l + r)
      ) (evalOpt x.rhs)) (evalOpt x.lhs)
  }
//│ evalOpt: (Lit & {v: int & 'b} | (Neg with {sub: 'a}) | (Plus with {lhs: 'a, rhs: 'a}) | Var as 'a) -> (NoneBecause | (Some['A .. 'A | 'b] with {v: 'b}) | (Some['A0 .. 'A0 | int] with {v: int}) | (Some['A1 .. 'A1 | int] with {v: int}))

evalOpt (Plus{lhs = Lit{v=2}; rhs = Lit{v=3}})
//│ res: NoneBecause | (Some['A .. 2 | 3 | 'A] with {v: 2 | 3}) | (Some['A0 .. 'A0 | int] with {v: int}) | (Some['A1 .. 'A1 | int] with {v: int})
