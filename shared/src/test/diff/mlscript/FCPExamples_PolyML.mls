:GeneralizeCurriedFunctions // TODO try out

// ============ Sec 1.1 ============

// i.e. auto
g = fun (f: forall 'a. 'a -> 'a) -> f f
g' = fun f -> f f
//│ g: (forall 'a. 'a -> 'a) -> (forall 'a. 'a -> 'a)
//│  = [Function: g]
//│ g': ('a -> 'b & 'a) -> 'b
//│   = [Function: g]

// i.e. auto id (FreezeML F5)
(fun (f: forall 'a. 'a -> 'a) -> f f) (fun x -> x)
(fun f -> f f) (fun x -> x)
//│ res: 'a -> 'a
//│    = [Function (anonymous)]
//│ res: 'a -> 'a
//│    = [Function (anonymous)]

// i.e. (fun auto -> auto id) auto
(fun g -> g (fun x -> x)) (fun f -> f f)
(fun g -> g (fun x -> x)) (fun (f: forall 'a. 'a -> 'a) -> f f)
//│ res: 'a -> 'a
//│    = [Function (anonymous)]
//│ res: 'a -> 'a
//│    = [Function (anonymous)]

// ============ Sec 1.2 ============

fun f -> f (g f)
fun f -> f (g' f)
//│ res: ((forall 'a. 'a -> 'a) -> 'b & 'a -> 'a) -> 'b
//│    = [Function: res]
//│ res: ('a -> 'b & 'c -> 'a & 'c) -> 'b
//│    = [Function: res]

// ============ Sec 4.1 ============

let f = fun x -> x in 
  let g = (fun x -> x) f in
    g g
//│ res: 'a -> 'a
//│    = [Function (anonymous)]

:NoJS

// ============ Sec 4.2 ============
// using FCP to solve method typing issues,
// which is overkill since it can be solved with subtyping and two levels of polymorphism...
// all of the following are typable in the main branch

// ------------ Dummy classes to represent the types in the examples ------------

class List[a]
  method Get: a
//│ Defined class List[+a]
//│ Declared List.Get: List['a] -> 'a

// Used to represent `::` in the papers
def cons[a]: a -> List[a] -> List[a]
//│ cons: 'a -> List['a] -> List['a]

// Used to represent `[]` in the papers
def nil: List['a]
//│ nil: List[nothing]

def match_list: forall 'a 'b. List['a] -> 'b -> ('a -> List['a] -> 'b) -> 'b
//│ match_list: List['a] -> 'b -> ('a -> List['a] -> 'b) -> 'b

// ------------ Type signatures for functions used in the examples ------------
// (I guess)

def mem[a]: a -> List[a] -> bool
rec def mem x l = match_list l false (fun head -> fun tail -> if eq head x then true else mem x tail)
//│ mem: anything -> List[?] -> bool
//│ anything -> List[?] -> bool
//│   <:  mem:
//│ anything -> List[?] -> bool

def fold_left[a, b]: (a -> b -> a) -> a -> List[b] -> a
rec def fold_left f x l = match_list l x (fun head -> fun tail -> fold_left f (f x head) tail)
//│ fold_left: ('a -> 'b -> 'a) -> 'a -> List['b] -> 'a
//│ ('a -> 'b -> ('a & 'c)) -> (forall 'd. ('a & 'd) -> List['b] -> ('d | 'c))
//│   <:  fold_left:
//│ ('a -> 'b -> 'a) -> 'a -> List['b] -> 'a

// ------------ polymorphic methods ------------

class Collection[a]: { l: List[a] }
  method Mem x = mem x this.l
  method Fold f x = fold_left f x this.l
//│ Defined class Collection[+a]
//│ Defined Collection.Mem: Collection[?] -> anything -> bool
//│ Defined Collection.Fold: Collection['a] -> ('b -> 'a -> ('b & 'c)) -> (forall 'd. ('b & 'd) -> ('c | 'd))

def coll_mem c x = c.Mem x
//│ coll_mem: Collection[?] -> anything -> bool

// typo in the paper? it was `fun x -> fun y -> ...`
def simple_and_double c =
  let l1 = c.Fold (fun y -> fun x -> cons x y) nil in
  let l2 = c.Fold (fun y -> fun x -> cons ((x, x),) y) nil in
  (l1, l2)
//│ simple_and_double: Collection[?] -> (List[nothing], List[(nothing, nothing,)],)

// ^ probably the simplification bug of https://github.com/hkust-taco/mlscript/issues/114

simple_and_double (Collection{l = cons 1 nil})
//│ res: (List[1], List[(nothing, nothing,)],)

:ns
simple_and_double
//│ res: forall 'b, 'a, 'a0. 'b -> (forall 'c, 'd, 'e. 'c, forall 'f, 'g, 'h, 'i. 'f,)
//│   where
//│     'f :> List['g] | (forall 'a1. List['a1])
//│     'g :> 'h | ('i, 'i,)
//│        <: 'h
//│     'c :> List['d] | (forall 'a1. List['a1])
//│     'd :> 'a | 'e
//│        <: 'e
//│     'e :> 'a
//│     'b <: Collection['a0] & Collection['a]

// ------------ subtyping & methods ------------

class Float
class Color
class Point: { x: Float; y: Float }
class ColorPoint: Point & { c: Color }
class Circle: { x: Float; y: Float; r: Float }
  method Distance: Point -> Float
//│ Defined class Float
//│ Defined class Color
//│ Defined class Point
//│ Defined class ColorPoint
//│ Defined class Circle
//│ Declared Circle.Distance: Circle -> Point -> Float

c = error : Circle
cp = error : ColorPoint
c.Distance cp
//│ c: Circle
//│ cp: ColorPoint
//│ res: Float
