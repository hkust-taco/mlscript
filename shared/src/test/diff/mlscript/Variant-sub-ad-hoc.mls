class None
class Some
//│ Defined class None
//│ Defined class Some

def Some v = Some{} with {v}
def None = None{}
//│ Some: 'a -> (Some & {v: 'a})
//│     = [Function: Some1]
//│ None: None
//│     = [Function: None1]

def flatMap = fun f -> fun opt ->
  case opt of { Some -> f opt.v | _ -> opt }
//│ flatMap: ('v -> 'a) -> (Some & {v: 'v} | 'a & ~#Some) -> 'a
//│        = [Function: flatMap]

f = fun x -> Some x
res = flatMap f (Some 1)
res = flatMap f None
//│ f: 'a -> (Some & {v: 'a})
//│  = [Function: f]
//│ res: Some & {v: 1}
//│    = Some { v: 1 }
//│ res: Some & {v: nothing} | None
//│    = None {}

class Lit
class Neg
class Var
class Plus
//│ Defined class Lit
//│ Defined class Neg
//│ Defined class Var
//│ Defined class Plus

:stats
rec def evalOpt = fun x -> case x of {
  | Lit ->
      Some x.v
  | Neg ->
      flatMap (fun s -> Some (0 - s)) (evalOpt x.sub)
  | Var ->
      None with {err = concat "free var: " x.nme}
  | Plus ->
      flatMap (fun l -> flatMap (fun r ->
        Some (l + r)
      ) (evalOpt x.rhs)) (evalOpt x.lhs)
  }
//│ evalOpt: 'a -> (Some & {v: int} | None & {err: string})
//│   where
//│     'a <: Lit & {v: int} | Neg & {sub: 'a} | Var & {nme: string} | Plus & {lhs: 'a, rhs: 'a}
//│        = [Function: evalOpt]
//│ constrain calls  : 533
//│ annoying  calls  : 156
//│ subtyping calls  : 3001

:stats
evalOpt (Plus{} with {lhs = Lit{} with {v=2}; rhs = Lit{} with {v=2}})
//│ res: Some & {v: int} | None & {err: string}
//│    = Some { v: 4 }
//│ constrain calls  : 88
//│ annoying  calls  : 34
//│ subtyping calls  : 653

