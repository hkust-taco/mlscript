:NoJS

class Leaf
class Node
class Some[A]: { value: A }
  method Id: A -> A
class None
//│ Defined class Leaf
//│ Defined class Node
//│ Defined class Some[=A]
//│ Declared Some.Id: Some['A] -> 'A -> 'A
//│ Defined class None

def Some: 'a -> Some['a]
def None: None
//│ Some: 'a -> Some['a]
//│ None: None

rec def f w = case w of
  Leaf -> None,
  Node ->
    let left = (w).left in
    let right = (w).right in
    let tmp0 = f (left,) in
    case tmp0 of
      None -> (
        let tmp1 = f (right,) in
        case tmp1 of
          None -> Some (0,),
          Some ->
            let m = (tmp1).value in
            Some (m,)
      ),
      Some ->
        let m = (tmp0).value in
        Some (m,)
//│ f: (Leaf | Node & 'b) -> (None | Some[in 'value & 'a | 'value & 'a0 | 'value & 'a1 out (0 | 'a) & (0 | 'value | 'a0) & (0 | 'value | 'a1)])
//│   where
//│     'b <: {left: Leaf | Node & 'b, right: Leaf | Node & 'b}

// * Minimizations:

rec def f w = case w of
  Node ->
    let left = (w).left in
    let tmp0 = f (left,) in
    case tmp0 of
      None -> Some (0,),
      Some ->
        let m = (tmp0).value in
        Some (m,)
//│ f: 'left -> Some[in 'value & 'a | 'value & 'a0 out (0 | 'a) & (0 | 'value | 'a0)]
//│   where
//│     'left <: Node & {left: 'left}

rec def f w = case w of
  Node ->
    let tmp0 = f w.left in
    case tmp0 of
      None -> Some 0,
      Some ->
        // Some tmp0.value // * produces different result!
        let m = tmp0.value in Some m
//│ f: 'b -> Some[in 'value & 'a | 'value & 'a0 out (0 | 'a) & (0 | 'value | 'a0)]
//│   where
//│     'b <: Node & {left: 'b}

def f w =
  let tmp0 = f w.left in
  case tmp0 of
    None -> Some 0,
    Some ->
      let m = tmp0.value in Some m
//│ f: 'b -> Some[in 'a | 'a0 out (0 | 'a) & (0 | 'a0)]
//│   where
//│     'b <: {left: Node & 'b}

